#---------------------------------------------------------------------------
## Author:      Dave Register
## DEB config from: antonm - Anton Martchukov <anton@martchukov.com>
## Update:      sethdart (Jean-Eudes Onfray)
##              with parts from balp (Anders Arnholm)
##***************************************************************************
## *   Copyright (C) 2010 by David S. Register                               *
## *   This program is free software; you can redistribute it and/or modify  *
## *   it under the terms of the GNU General Public License as published by  *
## *   the Free Software Foundation; either version 2 of the License, or     *
## *   (at your option) any later version.                                   *
## *                                                                         *
## *   This program is distributed in the hope that it will be useful,       *
## *   but WITHOUT ANY WARRANTY; without even the implied warranty of        *
## *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the         *
## *   GNU General Public License for more details.                          *
## *                                                                         *
## *   You should have received a copy of the GNU General Public License     *
## *   along with this program; if not, write to the                         *
## *   Free Software Foundation, Inc.,                                       *
## *   51 Franklin Street, Fifth Floor, Boston, MA 02110-1301,  USA.         *
## ***************************************************************************

#
#  Configuring opencpn
#  ===================
#
#  Sound:
#    - Without options, defaults to using SYSTEM_CMD_SOUND using platform
#      dependent command line tools such as aplay, afplay or mediaplayer.
#    - With ENABLE_SYSTEM_CMD_SOUND disabled falls back to wxSound backend.
#      This is known to be broken on at least Linux, and only supports .wav
#      files on all platforms.
#    - ENABLE_PORTAUDIO builds using [lib]portaudio if available.
#    - ENABLE_SYSTEM_CMD_SOUND builds using platform dependent command line
#      tools such as aplay, afplay or mediaplayer.
#    - ENABLE_SNDFILE adds libsndfile support to portaudio. libsndfile
#      supports a wide range of audio formats instead of .wav only.
#
#  Bundled libraries:
#    - USE_BUNDLED_LIBS forces use of external/bundled libraries including
#      wxsvg and liblz4.
#    - If found, the system tinyxml library is used unconditionally.

#  Documentation:
#    - BUNDLE_DOCS governs inclusion of docs in the package.
#    - Note that documentation is available in a separate download from
#      opencpn website.
#
#  Tidal/current and GSHHS wallpaper
#    - BUNDLE_TCDATA (boolean) includes tide/current harmonics data in package
#    - BUNDLE_GSHHS (NONE,MIN,[CRUDE],LOW,INTERMEDIATE,HIGH,FULL) governs the
#      wallpaper map used as last resort and world overview.
#
#  To build for android, use something like:
#      $cmake -DUSE_GARMINHOST=OFF -D_wx_selected_config=androideabi-qt
#          -DCMAKE_TOOLCHAIN_FILE=../buildandroid/build_android.cmake
#          -DwxQt_Build=build_android_53 -DwxQt_Base=/home/dsr/Projects/wxqt/wxWidgets
#          -DQt_Base /home/dsr/Qt/5.3) ..
#TODO:
# - Build profiles
# - cross platform compiling
# - Better USE_BUNDLED_LIBS option
# - test with Win & OSX
# USE_GLU_TESS
# USE_GLU_DLL
# I also find it deficient in some areas. For instance, I cannot make it
# output a VS project with certain compile switches set as desired,
# namely /MT vs /MD. This means I must manually set this and other compiler
# options after the CMake step. Sometimes I forget. Grrr...
# set /MT for Release build, and /MTd for debug.


CMAKE_MINIMUM_REQUIRED(VERSION 3.1.1)
IF (COMMAND cmake_policy)
  if (POLICY CMP0043)
    CMAKE_POLICY(SET CMP0043 NEW)
  endif (POLICY CMP0043)
  if (POLICY CMP0025)
    CMAKE_POLICY(SET CMP0025 NEW)
  endif ()
  if (POLICY CMP0077)
      CMAKE_POLICY(SET CMP0077 NEW)
  endif ()
ENDIF (COMMAND cmake_policy)

# Prefer libGL.so to libOpenGL.so, see CMP0072
set (OpenGL_GL_PREFERENCE  "LEGACY")

PROJECT(OpenCPN)

SET(CMAKE_VERBOSE_MAKEFILE ON)
message(STATUS "cmake version: ${CMAKE_VERSION}")

IF(APPLE)
  SET(PACKAGE_NAME OpenCPN)
ELSE(APPLE)
  SET(PACKAGE_NAME opencpn)
ENDIF(APPLE)

# Locations where cmake looks for cmake modules.
SET(CMAKE_MODULE_PATH
  ${CMAKE_SOURCE_DIR}/build
  ${CMAKE_SOURCE_DIR}/
  ${CMAKE_SOURCE_DIR}/cmake
)

INCLUDE( FindPkgConfig )
INCLUDE( CMakeDependentOption )

#
# Options
#

# If on Windows or Apple, build the monolithic package, to override use a
# non-sense values from the commandline (due to cmake inability to
# distinguish between not set and set to FALSE):
#    cmake -DBUNDLE_DOCS=BLAH -DBUNDLE_TCDATA=BLAH -DBUNDLE_GSHHS=BLAH ..
MACRO(BUNDLE_DOCS_OPT onoff)
  OPTION(BUNDLE_DOCS ${onoff} "Include documentation in package")
ENDMACRO(BUNDLE_DOCS_OPT)

MACRO(BUNDLE_TCDATA_OPT onoff)
  OPTION(BUNDLE_TCDATA "${onoff}" "Include tide/current harmonics data in package")
ENDMACRO(BUNDLE_TCDATA_OPT)

MACRO(BUNDLE_GSHHS_OPT value)
  SET(BUNDLE_GSHHS "${value}" CACHE STRING
      "Include GSHHS wallpaper map in package (NONE,MIN,[CRUDE],LOW,INTERMEDIATE,HIGH,FULL)")
ENDMACRO(BUNDLE_GSHHS_OPT)

SET(PACKAGE_RELEASE "1" CACHE STRING "Package release number")

OPTION (USE_GL "Enable OpenGL support" ON)

IF(MSVC OR (NOT APPLE AND NOT QT_ANDROID AND NOT MINGW))
    OPTION (OCPN_USE_CRASHREPORT "Enable crash reporting" ON)
ELSE()
    SET(OCPN_USE_CRASHREPORT FALSE)
ENDIF()

if (NOT WIN32)
  set(PA_DEFAULT "ON")
endif ()
OPTION(ENABLE_PORTAUDIO
  "Use portaudio(3) to play sounds if available"  ${PA_DEFAULT}
)

CMAKE_DEPENDENT_OPTION(ENABLE_SNDFILE
    "Use libsndfile for portaudio if available." ON 
    "ENABLE_PORTAUDIO" ON
)

OPTION (USE_S57 "Enable S57 ENC support" ON)

OPTION (USE_GARMINHOST "Enable Garmin Host Mode support" ON)

set (WXWIDGETS_FORCE_VERSION CACHE VERSION
     "Force usage of a specific wxWidgets version.")

set (WXWIDGETS_OPTIONS CACHE STRING
    "Additional flags to wxWidgets_CONFIG_OPTIONS")

IF(WIN32 AND NOT UNIX)
  OPTION (BUNDLE_WXDLLS "Bundle the prebuilt WX DLLs" ON)
  OPTION (BUNDLE_VCDLLS "Bundle the VC redistributable libraries" ON)
ENDIF()

if ( CMAKE_VERSION VERSION_GREATER 3.4 )
  OPTION(ENABLE_CLANG_TIDY "Add clang-tidy automatically to builds" OFF)
  set(ENABLE_SANITIZER "none" CACHE STRING "Add clang sanitizer to the build")
endif()

if ( CMAKE_VERSION VERSION_GREATER 3.9 )
    OPTION(ENABLE_CPPCHECK "Add cppcheck automatically to builds" OFF)
endif()

IF( NOT CMAKE_BUILD_TYPE )
  SET(CMAKE_BUILD_TYPE RelWithDebInfo CACHE STRING
      "Choose type of build: None Debug Release RelWithDebInfo MinSizeRel."
      FORCE
  )
ENDIF( NOT CMAKE_BUILD_TYPE )

OPTION(OCPN_USE_LIBCPP "Use libc++ instead of libstdc++ on macOS" ON)

OPTION(OCPN_USE_EXTERN_CURL "Use external libcurl" OFF)

IF (NOT CMAKE_INSTALL_PREFIX)
  SET(CMAKE_INSTALL_PREFIX ${TENTATIVE_PREFIX})
ENDIF (NOT CMAKE_INSTALL_PREFIX)

if (NOT ${CMAKE_SYSTEM_NAME} STREQUAL "Linux")
  set(BUNDLED_LIBS_DEFAULT "ON")
endif ()
OPTION(USE_BUNDLED_LIBS "Use bundled libraries instead of system's"
   ${BUNDLED_LIBS_DEFAULT}
)

OPTION(OCPN_NEW_SERIAL "Use new serial communication implementation" ON)
OPTION(OCPN_USE_CURL "Use Curl libraries" ON)
OPTION(OCPN_USE_SVG "Use SVG graphics" ON)
OPTION(OCPN_USE_LZMA "Use LZMA for chart compression" ON)
OPTION(OCPN_CI_BUILD "Use CI build versioning rules" OFF)

OPTION(ENABLE_SYSTEM_CMD_SOUND
       "Use aplay(1), afplay(1) etc. to play sounds if available" ON)


#
# Language, compiler and static checkers setup
#

include(GetArch)
GetArch()
MESSAGE (STATUS "*** Build Architecture is ${ARCH}")

set (CMAKE_CXX_STANDARD 11)
message(STATUS "Setting C++11 standard via cmake standard mechanism")
IF(NOT MSVC)
   SET(OBJ_VISIBILITY "-fvisibility=hidden" )
ENDIF()

if (CMAKE_VERSION VERSION_GREATER 3.4)
  if (ENABLE_CLANG_TIDY)
    find_program (CLANG_TIDY_EXE NAMES "clang-tidy" PATHS /usr/local/opt/llvm/bin )
    if (CLANG_TIDY_EXE)
      message(STATUS "clang-tidy found: ${CLANG_TIDY_EXE}")
      #For more, see http://clang.llvm.org/extra/clang-tidy/
      #set(CLANG_TIDY_CHECKS "-*,modernize-*")
      set(CLANG_TIDY_CHECKS "-*,performance-*")
      set(CMAKE_CXX_CLANG_TIDY "${CLANG_TIDY_EXE};-checks=${CLANG_TIDY_CHECKS};-header-filter='${CMAKE_SOURCE_DIR}/*'"
        CACHE STRING "" FORCE)
    else()
      message(AUTHOR_WARNING "clang-tidy not found!")
      set(CMAKE_CXX_CLANG_TIDY "" CACHE STRING "" FORCE) # delete it
    endif()
  endif()
endif()

if (CMAKE_VERSION VERSION_GREATER 3.4)
  # Add support for address etc sanitizers, part 1/2
  # (other half after ADD_EXECUTABLE)
  set_property(CACHE ENABLE_SANITIZER PROPERTY
               STRINGS none address memory thread undefined)
  if (NOT "${ENABLE_SANITIZER}" MATCHES "none")
    add_compile_options(-fsanitize=${ENABLE_SANITIZER})
  endif()
endif()

if ( CMAKE_VERSION VERSION_GREATER 3.9 )
  if (ENABLE_CPPCHECK)
    find_program (CPPCHECK_EXECUTABLE NAMES "cppcheck" )
    set(CMAKE_CXX_CPPCHECK ${CPPCHECK_EXECUTABLE})
  endif()
endif()
message(STATUS "Default compiler options:")
message(STATUS "CMAKE_CXX_FLAGS: ${CMAKE_CXX_FLAGS}")
message(STATUS "CMAKE_CXX_FLAGS_DEBUG: ${CMAKE_CXX_FLAGS_DEBUG}")
message(STATUS "CMAKE_CXX_FLAGS_MINSIZEREL: ${CMAKE_CXX_FLAGS_MINSIZEREL}")
message(STATUS "CMAKE_CXX_FLAGS_RELEASE: ${CMAKE_CXX_FLAGS_RELEASE}")
message(STATUS 
   "CMAKE_CXX_FLAGS_RELWITHDEBINFO: ${CMAKE_CXX_FLAGS_RELWITHDEBINFO}")
#ADD_COMPILE_OPTIONS( "-Wall" "-ansi" "-pedantic" "-Wno-variadic-macros" )
#TODO: Should we use  -fno-stack-protector
#  IF NOT DEBUGGING CFLAGS="-O2 -march=native"
IF(NOT WIN32 AND NOT APPLE )
  ADD_COMPILE_OPTIONS( "-Wall"
      "-Wno-unused"
      "-fexceptions"
      "-rdynamic"
      "-fno-strict-aliasing"
  )
  IF (CMAKE_BUILD_TYPE MATCHES "Debug")
    ADD_COMPILE_OPTIONS("-O0")
  ENDIF ()
  ADD_COMPILE_OPTIONS("-Wno-deprecated-declarations")
  # TODO: Enable deprecation warnings again after the 5.0.0 cycle

  ADD_DEFINITIONS( " -DPREFIX=\\\"${CMAKE_INSTALL_PREFIX}\\\"")
  # profiling with gprof
  #   ADD_COMPILE_OPTIONS( -pg )
  #   SET(CMAKE_EXE_LINKER_FLAGS -pg)
  # profiling with gcov
  #   ADD_COMPILE_OPTIONS( "-fprofile-arcs -ftest-coverage" )
  #   SET(EXTRA_LIBS ${EXTRA_LIBS} "gcov")
ENDIF(NOT WIN32 AND NOT APPLE)

IF(MINGW)
    ADD_COMPILE_OPTIONS( "-Wall"
        "-Wno-unused"
        "-Wno-cpp"
        "-fexceptions"
        "-fno-strict-aliasing"
    )
    ADD_DEFINITIONS("-DPSAPI_VERSION=1")
    ADD_DEFINITIONS("-DUNICODE")
ENDIF(MINGW)

IF( APPLE )
    ADD_COMPILE_OPTIONS( "-Wall"
        "-Wno-unused"
        "-fexceptions"
        "-Wno-overloaded-virtual"
        "-fno-strict-aliasing"
        "-Wno-deprecated"
        "-Wno-deprecated-declarations"
        "-Wno-unknown-pragmas"
        "-D_WCHAR_H_CPLUSPLUS_98_CONFORMANCE_"
    )
ENDIF(APPLE)

IF(APPLE)
  SET(CMAKE_C_FLAGS "-O2 -arch ${ARCH}")
  SET(CMAKE_C_FLAGS_DEBUG "-g -O0 -arch ${ARCH}")
  SET(CMAKE_C_FLAGS_MINSIZEREL "-O2 -arch ${ARCH}")
  SET(CMAKE_C_FLAGS_RELEASE "-O3 -arch ${ARCH}")
  SET(CMAKE_C_FLAGS_RELWITHDEBINFO "-g -O3 -arch ${ARCH}")

  IF(OCPN_USE_LIBCPP)
    SET(OCPN_LIBCPP "-stdlib=libc++")
  ENDIF(OCPN_USE_LIBCPP)
  SET(CMAKE_CXX_FLAGS "-O2 ${OCPN_LIBCPP} -arch ${ARCH}")
  SET(CMAKE_CXX_FLAGS_DEBUG "-g -O0 ${OCPN_LIBCPP} -arch ${ARCH}")
  SET(CMAKE_CXX_FLAGS_MINSIZEREL "-O2 ${OCPN_LIBCPP} -arch ${ARCH}")
  SET(CMAKE_CXX_FLAGS_RELEASE "-O3 ${OCPN_LIBCPP} -arch ${ARCH}")
  SET(CMAKE_CXX_FLAGS_RELWITHDEBINFO "-g -O3 ${OCPN_LIBCPP} -arch ${ARCH}")

  SET(CMAKE_EXE_LINKER_FLAGS "-O2 ${OCPN_LIBCPP} -arch ${ARCH}")
  SET(CMAKE_SHARED_LINKER_FLAGS "-O2 ${OCPN_LIBCPP} -arch ${ARCH}")
  SET(CMAKE_MODULE_LINKER_FLAGS "-O2 ${OCPN_LIBCPP} -arch ${ARCH}")
ENDIF(APPLE)


IF(MSVC)
  ADD_DEFINITIONS(-D__MSVC__)
  ADD_DEFINITIONS(-D_CRT_NONSTDC_NO_DEPRECATE -D_CRT_SECURE_NO_DEPRECATE)
  ADD_DEFINITIONS(-DPSAPI_VERSION=1)
ENDIF(MSVC)

IF(MSVC)
  SET(CMAKE_C_FLAGS_DEBUG               "/MP /MDd /Ob0 /Od  /D_DEBUG  /Zi /RTC1" )
  SET(CMAKE_C_FLAGS_MINSIZEREL          "/MP /MD  /O1  /Ob1 /D NDEBUG")
  SET(CMAKE_C_FLAGS_RELEASE             "/MP /MD  /O2  /Ob2 /D NDEBUG /Zi /wd8051 /wd4068")
  SET(CMAKE_C_FLAGS_RELWITHDEBINFO      "/MP /MD  /O2  /Ob1 /D NDEBUG /Zi")
  SET(CMAKE_CXX_FLAGS_DEBUG             "/MP /MDd /Ob0 /Od  /D_DEBUG  /Zi /RTC1 /EHa")
  SET(CMAKE_CXX_FLAGS_MINSIZEREL        "/MP /MD  /O1  /Ob1 /D NDEBUG /EHa")
  SET(CMAKE_CXX_FLAGS_RELEASE           "/MP /MD  /O2  /Ob2 /D NDEBUG /Zi /EHa")
  SET(CMAKE_CXX_FLAGS_RELWITHDEBINFO    "/MP /MD  /O2  /Ob1 /D NDEBUG /Zi /EHa" )
  SET(CMAKE_EXE_LINKER_FLAGS /DEBUG)
ENDIF(MSVC)

IF(QT_ANDROID)
  SET(CMAKE_BUILD_TYPE Debug)
  ADD_DEFINITIONS(-D__WXQT__)
  ADD_DEFINITIONS(-DOCPN_USE_WRAPPER)
  ADD_DEFINITIONS(-D__OCPN__ANDROID__)
  ADD_DEFINITIONS(-DANDROID)
  SET(CMAKE_CXX_FLAGS "-pthread -fPIC -s -O2")
ENDIF(QT_ANDROID)


SET(LINUX_LIB_PATHS
    /usr/local/lib
    /usr/local/lib64
    /usr/lib/i386-linux-gnu
    /usr/lib/x86_64-linux-gnu
    /usr/lib
    /usr/lib64
)

SET(PREFIX_BIN bin)
SET(PREFIX_INCLUDE include)
SET(PREFIX_DATA share)
SET(PREFIX_PKGDATA ${PREFIX_DATA}/${PACKAGE_NAME})
SET(PREFIX_LIB "${CMAKE_INSTALL_PREFIX}/${LIB_INSTALL_DIR}")

#
# Version handling
#
INCLUDE( ${CMAKE_SOURCE_DIR}/VERSION.cmake )

IF(OCPN_CI_BUILD)
  INCLUDE(Utils)
  TODAY(DATE)
  COMMIT_ID(COMMIT)
  SET(VERSION_TAIL "+${COMMIT}")
  SET(VERSION_DATE "${DATE}")
ENDIF(OCPN_CI_BUILD)

SET(PACKAGE_VERSION
    "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}${VERSION_TAIL}")


#  Detect the Apple Version of the build machine
IF (APPLE)
  EXEC_PROGRAM(uname ARGS -v  OUTPUT_VARIABLE DARWIN_VERSION)
  STRING(REGEX MATCH "[0-9]+" DARWIN_VERSION ${DARWIN_VERSION})
  MESSAGE(STATUS "*** Building on DARWIN_VERSION=${DARWIN_VERSION}")
  IF (DARWIN_VERSION GREATER 11)
    SET(APPLE_MODERN 1 INTERNAL)
  ENDIF (DARWIN_VERSION GREATER 11)
ENDIF(APPLE)

MESSAGE (STATUS "*** Staging to build ${PACKAGE_NAME} ${PACKAGE_VERSION} ***")

#
#  Bundled data: docs, tcdata, gshhs
#

IF(APPLE OR WIN32)
  BUNDLE_DOCS_OPT("ON")
  BUNDLE_TCDATA_OPT("ON")
  BUNDLE_GSHHS_OPT("CRUDE")
ELSE(APPLE OR WIN32)
  BUNDLE_DOCS_OPT("OFF")
  BUNDLE_TCDATA_OPT("OFF")
  BUNDLE_GSHHS_OPT("NONE")
ENDIF(APPLE OR WIN32)

IF(NOT WIN32 AND NOT APPLE AND NOT QT_ANDROID)
  OPTION(OCPN_FORCE_GTK3 "Force the build to use GTK3" OFF)
ENDIF ()

IF(BUNDLE_DOCS MATCHES "ON")
  MESSAGE (STATUS "*** Package will include documentation ***")
  IF(UNIX AND NOT APPLE)
    SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_docs ON")
  ENDIF(UNIX AND NOT APPLE)
ELSE(BUNDLE_DOCS MATCHES "ON")
  MESSAGE (STATUS "*** Package will NOT include documentation ***")
  IF(UNIX AND NOT APPLE)
    SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_docs OFF")
  ENDIF(UNIX AND NOT APPLE)
ENDIF(BUNDLE_DOCS MATCHES "ON")

MESSAGE (STATUS "")
MESSAGE (STATUS "*** Staging to build ${PACKAGE_NAME}  ***")
MESSAGE (STATUS "*** Build type: ${CMAKE_BUILD_TYPE}")
MESSAGE (STATUS "*** Will install to ${CMAKE_INSTALL_PREFIX}  ***")

  ## Compiler flags
  #  if(CMAKE_COMPILER_IS_GNUCXX)
  #      set(CMAKE_CXX_FLAGS "-O2")        ## Optimize
  #  endif()
  set(CMAKE_EXE_LINKER_FLAGS "-s")  ## Strip binary

IF(QT_ANDROID)
  INCLUDE_DIRECTORIES("${Qt_Base}/android_armv7/include/QtCore")
  INCLUDE_DIRECTORIES("${Qt_Base}/android_armv7/include")
  INCLUDE_DIRECTORIES("${Qt_Base}/android_armv7/include/QtWidgets")
  INCLUDE_DIRECTORIES("${Qt_Base}/android_armv7/include/QtGui")
  INCLUDE_DIRECTORIES("${Qt_Base}/android_armv7/include/QtOpenGL")
  INCLUDE_DIRECTORIES("${Qt_Base}/android_armv7/include/QtTest")

  INCLUDE_DIRECTORIES(
      "${wxQt_Base}/${wxQt_Build}/lib/wx/include/arm-linux-androideabi-qt-unicode-static-3.1")
  INCLUDE_DIRECTORIES("${wxQt_Base}/include")

  ADD_DEFINITIONS(-DQT_WIDGETS_LIB)
ENDIF(QT_ANDROID)

#IF(NOT WIN32)
#OPTION (USE_GPSD "Enable GPSD Library" ON)
#IF (USE_GPSD)
#      message (STATUS "GPSD Library support: enabled")
#
##     FIND_PACKAGE(gps QUIET)
#     FIND_PACKAGE(gps)
#      IF (GPS_FOUND)
#            message (STATUS "Building with libgps includes")
#            INCLUDE_DIRECTORIES(${GPS_INCLUDE_DIR})
#            ADD_DEFINITIONS(${GPS_DEFINITIONS})
#            ADD_DEFINITIONS(-DBUILD_WITH_LIBGPS)
#            message (STATUS "GPS library include location is ${GPS_INCLUDE_DIR}")
##            SET(EXTRA_LIBS ${EXTRA_LIBS} ${GPS_LIBRARY})
#      ELSE (GPS_FOUND)
#            message (STATUS "Gps library not found")
#      ENDIF (GPS_FOUND)
#ENDIF (USE_GPSD)
#ENDIF (NOT WIN32)

SET(HDRS
    include/ocpn_types.h
    include/ocpndc.h
    include/chart1.h
    include/bbox.h
    include/ocpn_pixel.h
    include/chartdb.h
    include/chartdbs.h
    include/chartimg.h
    include/ChartDataInputStream.h
    include/chcanv.h
    include/timers.h
    include/emboss_data.h
    include/AISTargetQueryDialog.h
    include/AIS_Bitstring.h
    include/AISTargetListDialog.h
    include/OCPNListCtrl.h
    include/AISTargetAlertDialog.h
    include/AIS_Decoder.h
    include/AIS_Target_Data.h
    include/DetailSlider.h
    include/GoToPositionDialog.h
    include/RolloverWin.h
    include/S57QueryDialog.h
    include/S57ObjectDesc.h
    include/S57Light.h
    include/S57ClassRegistrar.h
    include/MarkIcon.h
    include/SendToGpsDlg.h
    include/TCWin.h
    include/tide_time.h
    include/ChInfoWin.h
    include/ocpCursor.h
    include/Quilt.h
    include/Hyperlink.h
    include/NavObjectCollection.h
    include/RoutePoint.h
    include/Route.h
    include/Track.h
    include/SelectItem.h
    include/Select.h
    include/FontMgr.h
    include/FontDesc.h
    include/vector2D.h
    include/NMEALogWindow.h
    include/WindowDestroyListener.h
    include/TTYWindow.h
    include/TTYScroll.h
    include/Layer.h
    include/concanv.h
    include/cutil.h
    include/georef.h
    include/navutil.h
    include/routeman.h
    include/routemanagerdialog.h
    include/MarkInfo.h
    include/RoutePropDlg.h
    include/RoutePropDlgImpl.h
    include/routeprintout.h
    include/trackprintout.h
    include/PositionParser.h
    include/printtable.h
    include/piano.h
    include/tcmgr.h
    include/Station_Data.h
    include/IDX_entry.h
    include/TC_Error_Code.h
    include/TCDataFactory.h
    include/TCDS_Ascii_Harmonic.h
    include/TCDS_Binary_Harmonic.h
    include/TCDataSource.h
    include/thumbwin.h
    include/options.h
    include/gshhs.h
    include/kml.h
    include/undo.h
    include/AboutFrame.h
    include/AboutFrameImpl.h
    include/ais.h
    include/pluginmanager.h
    include/ocpn_plugin.h
    include/styles.h
    include/toolbar.h
    include/compass.h
    include/geodesic.h
    include/datastream.h
    include/OCPN_DataStreamEvent.h
    include/ConnectionParams.h
    include/OCP_DataStreamInput_Thread.h
    include/dsPortType.h
    include/multiplexer.h
    include/OCPNRegion.h
    include/LLRegion.h
    include/TrackPropDlg.h
    include/LinkPropDlg.h
    include/viewport.h
    include/canvasMenu.h
    include/OCPNPlatform.h
    include/S57Sector.h
    include/FlexHash.h
    include/iENCToolbar.h
    include/mbtiles.h
    ${CMAKE_BINARY_DIR}/include/config.h
)

SET(SRCS
    src/chart1.cpp
    src/bbox.cpp
    src/ocpn_pixel.cpp
    src/ocpndc.cpp
    src/chartdb.cpp
    src/chartdbs.cpp
    src/chartimg.cpp
    src/ChartDataInputStream.cpp
    src/chcanv.cpp
    src/ocpCursor.cpp
    src/TCWin.cpp
    src/S57QueryDialog.cpp
    src/GoToPositionDialog.cpp
    src/DetailSlider.cpp
    src/RolloverWin.cpp
    src/ChInfoWin.cpp
    src/AISTargetQueryDialog.cpp
    src/AIS_Bitstring.cpp
    src/AISTargetListDialog.cpp
    src/AISTargetAlertDialog.cpp
    src/AIS_Decoder.cpp
    src/AIS_Target_Data.cpp
    src/OCPNListCtrl.cpp
    src/Quilt.cpp
    src/Hyperlink.cpp
    src/NavObjectCollection.cpp
    src/RoutePoint.cpp
    src/Route.cpp
    src/Track.cpp
    src/SelectItem.cpp
    src/Select.cpp
    src/FontMgr.cpp
    src/FontDesc.cpp
    src/NMEALogWindow.cpp
    src/TTYWindow.cpp
    src/TTYScroll.cpp
    src/SendToGpsDlg.cpp
    src/Layer.cpp
    src/concanv.cpp
    src/cutil.cpp
    src/georef.cpp
    src/navutil.cpp
    src/routeman.cpp
    src/routemanagerdialog.cpp
    src/MarkInfo.cpp
    src/RoutePropDlg.cpp
    src/RoutePropDlgImpl.cpp
    src/routeprintout.cpp
    src/trackprintout.cpp
    src/PositionParser.cpp
    src/printtable.cpp
    src/piano.cpp
    src/tcmgr.cpp
    src/Station_Data.cpp
    src/IDX_entry.cpp
    src/TCDataFactory.cpp
    src/TCDS_Ascii_Harmonic.cpp
    src/TCDS_Binary_Harmonic.cpp
    src/TCDataSource.cpp
    src/thumbwin.cpp
    src/options.cpp
    src/gshhs.cpp
    src/kml.cpp
    src/undo.cpp
    src/AboutFrame.cpp
    src/AboutFrameImpl.cpp
    src/ais.cpp
    src/pluginmanager.cpp
    src/styles.cpp
    src/toolbar.cpp
    src/compass.cpp
    src/geodesic.cpp
    src/datastream.cpp
    src/OCPN_DataStreamEvent.cpp
    src/ConnectionParams.cpp
    src/OCP_DataStreamInput_Thread.cpp
    src/multiplexer.cpp
    src/pugixml.cpp
    src/OCPNRegion.cpp
    src/LLRegion.cpp
    src/TrackPropDlg.cpp
    src/LinkPropDlg.cpp
    src/viewport.cpp
    src/canvasMenu.cpp
    src/OCPNPlatform.cpp
    src/FlexHash.cpp
    src/iENCToolbar.cpp
    src/mbtiles.cpp
    src/ConfigMgr.cpp
    src/CanvasOptions.cpp
    src/MUIBar.cpp
    src/OCPN_AUIManager.cpp
    src/CanvasConfig.cpp
    src/garmin_wrapper.h
)

IF (USE_GARMINHOST)
    SET(SRCS ${SRCS} src/garmin_wrapper.cpp)
ENDIF()

IF(APPLE)
    IF (DARWIN_VERSION LESS 16)
        message(STATUS
            "DarkMode not included, requires Mac build host Darwin >= 16")
    ELSE ()
        SET(SRCS ${SRCS}
            src/DarkMode.mm
            src/DarkMode.h
        )
    ENDIF ()
    SET(SRCS ${SRCS} src/MacSound.mm src/MacSound.h)
ENDIF()

IF(NOT WIN32 AND NOT APPLE AND NOT QT_ANDROID AND OCPN_USE_CRASHREPORT)
  SET(HDRS ${HDRS} include/crashprint.h )
  SET(SRCS ${SRCS} src/crashprint.cpp)
ENDIF(NOT WIN32 AND NOT APPLE AND NOT QT_ANDROID AND OCPN_USE_CRASHREPORT)

if (APPLE)
    ADD_EXECUTABLE(${PACKAGE_NAME} MACOSX_BUNDLE ${HDRS} ${SRCS})
elseif (WIN32)
    ADD_EXECUTABLE(${PACKAGE_NAME} WIN32 ${HDRS} ${SRCS})
else ()
    ADD_EXECUTABLE(${PACKAGE_NAME} ${HDRS} ${SRCS})
endif ()

add_library(_opencpn INTERFACE)    #  plugin link target.
target_link_libraries(_opencpn INTERFACE ${PACKAGE_NAME})
target_include_directories(_opencpn
    INTERFACE ${CMAKE_CURRENT_SOURCE_DIR}/include
)
add_library(ocpn::opencpn ALIAS _opencpn)

set_target_properties(${PACKAGE_NAME} PROPERTIES
    ENABLE_EXPORTS 1
    OUTPUT_NAME opencpn
    ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
)

target_include_directories(${PACKAGE_NAME}
      PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/include
)
IF(APPLE)
  target_include_directories(${PACKAGE_NAME} 
      PRIVATE /usr/X11/include /usr/X11/include/GL
  )
ENDIF ()


FIND_PACKAGE(Gettext REQUIRED)

add_subdirectory(libs/ssl_sha1)
target_link_libraries(${PACKAGE_NAME} PRIVATE ssl::sha1)

#
# Linux: set up GTK
#
IF (NOT WIN32 AND NOT APPLE AND NOT QT_ANDROID)
  include(OcpnFindGtk)
  target_link_libraries(${PACKAGE_NAME} PRIVATE ocpn::gtk)
ENDIF (NOT WIN32 AND NOT APPLE AND NOT QT_ANDROID)

# Search for opengles, short of running a program to test the speed
# of acceleration, I simply use gles on "native linux" arm systems
IF(DEFINED _wx_selected_config)
  MESSAGE (STATUS "selected config ${_wx_selected_config}")
  IF(_wx_selected_config MATCHES "androideabi-qt")
    MESSAGE (STATUS "Building for wxQt-Android")
    MESSAGE (STATUS "Qt_Base: " ${Qt_Base})
    MESSAGE (STATUS "wxQt_Base/Build: " ${wxQt_Base} "/" ${wxQt_Build})
    SET(QT_ANDROID "ON")
  ENDIF(_wx_selected_config MATCHES "androideabi-qt")
ENDIF(DEFINED _wx_selected_config)


IF((_wx_selected_config MATCHES "qt-armv7"))
  SET(wxWidgets_USE_LIBS base core xml html adv aui)
ELSE()
  SET(wxWidgets_USE_LIBS net xml html adv aui core base)
ENDIF()

IF (ARCH MATCHES "arm*" AND (NOT QT_ANDROID) )
  find_path(OPENGLESv1_INCLUDE_DIR GLES/gl.h )
  IF (OPENGLESv1_INCLUDE_DIR)
    MESSAGE (STATUS "Found OpenGLESv1")
    ADD_DEFINITIONS(-DocpnUSE_GL)   # ocpnUSE_GLES is in config.h

    SET(OPENGLES_FOUND "YES")
    SET(OPENGL_FOUND "YES")

    add_subdirectory(libs/glshim)

    SET(OPENGL_LIBRARIES "GL_static" "EGL" "X11" "drm"  )
  ENDIF()
ENDIF()

IF (OPENGLES_FOUND)
  SET(wxWidgets_USE_LIBS ${wxWidgets_USE_LIBS} gl )
ENDIF ()

IF ((NOT OPENGLES_FOUND) AND (NOT QT_ANDROID))
  IF(USE_GL)
    FIND_PACKAGE(OpenGL)
  ELSE(USE_GL)
    MESSAGE (STATUS "OpenGL disabled by option USE_GL..." )
  ENDIF(USE_GL)

  IF(OPENGL_FOUND)
    SET(wxWidgets_USE_LIBS gl ${wxWidgets_USE_LIBS} )
    target_include_directories(${PACKAGE_NAME} PRIVATE ${OPENGL_INCLUDE_DIR})

    MESSAGE (STATUS "Found OpenGL...." )
    MESSAGE (STATUS "    GL Lib: " ${OPENGL_LIBRARIES})
    MESSAGE (STATUS "    GL Include: " ${OPENGL_INCLUDE_DIR})
    ADD_DEFINITIONS(-DocpnUSE_GL)

    # We need to remove GLU from the OPENGL_LIBRARIES list
    FOREACH (_currentLibFile ${OPENGL_LIBRARIES})
      #MESSAGE (STATUS "    Lib File: " ${_currentLibFile})
      SET(UCNAME ${_currentLibFile})
      string(TOUPPER ${UCNAME} UCNAME)
      IF(NOT ${UCNAME} MATCHES   "(.*)GLU(.*)")
        SET(REVISED_OPENGL_LIBRARIES
            ${_currentLibFile} ${REVISED_OPENGL_LIBRARIES})
      ENDIF()
    ENDFOREACH (_currentLibFile )

    target_link_libraries(${PACKAGE_NAME} PRIVATE ${REVISED_OPENGL_LIBRARIES})
    MESSAGE (STATUS "    Revised GL Lib: " ${REVISED_OPENGL_LIBRARIES})

  ELSE(OPENGL_FOUND)
    MESSAGE (STATUS "OpenGL not found..." )
  ENDIF(OPENGL_FOUND)

ENDIF()

IF(NOT QT_ANDROID)
  # Find wxWidgets here, and the setting get inherited by all plugins.
  # These options can be used to set the linux widgets build type
  SET( wxWidgets_USE_DEBUG OFF)
  SET( wxWidgets_USE_UNICODE ON)
  SET( wxWidgets_USE_UNIVERSAL OFF)
  SET( wxWidgets_USE_STATIC OFF)

  if(WXWIDGETS_FORCE_VERSION)
    set (wxWidgets_CONFIG_OPTIONS --version=${WXWIDGETS_FORCE_VERSION})
  endif()
  set(wxWidgets_CONFIG_OPTIONS
      ${wxWidgets_CONFIG_OPTIONS} ${WXWIDGETS_OPTIONS})


  IF(MSVC)
    # Exclude wxexpat.lib, since we use our own version.
    # Other things are excluded as well, but we don't need them
    SET(wxWidgets_EXCLUDE_COMMON_LIBRARIES TRUE)
  ENDIF(MSVC)

  IF(GTK2_FOUND)
    set(wxWidgets_CONFIG_OPTIONS ${wxWidgets_CONFIG_OPTIONS} --toolkit=gtk2)
  ELSEIF(GTK3_FOUND)
    set(wxWidgets_CONFIG_OPTIONS ${wxWidgets_CONFIG_OPTIONS} --toolkit=gtk3)
  ENDIF ()

  FIND_PACKAGE(wxWidgets REQUIRED)
  IF(MSYS)
    # Convert msys to windows paths, and handle the missing /usr
    STRING(REGEX REPLACE "/usr/local" ";C:/MinGW/msys/1.0/local"
           wxWidgets_INCLUDE_DIRS "${wxWidgets_INCLUDE_DIRS}")
  ENDIF(MSYS)
  INCLUDE(${wxWidgets_USE_FILE})

  MESSAGE (STATUS "Found wxWidgets..." )
  MESSAGE (STATUS " wxWidgets Include: ${wxWidgets_INCLUDE_DIRS}")
  MESSAGE (STATUS " wxWidgets Libraries: ${wxWidgets_LIBRARIES}")

  # We need to remove GLU from the wxWidgets_LIBRARIES list
  # It only appears to get on the list for MSW...
  FOREACH (_currentLibFile ${wxWidgets_LIBRARIES})
    SET(UCNAME ${_currentLibFile})
    string(TOUPPER ${UCNAME} UCNAME)
    IF(NOT ${UCNAME} MATCHES   "(.*)GLU(.*)")
      SET( REVISED_wxWidgets_LIBRARIES  ${REVISED_wxWidgets_LIBRARIES} ${_currentLibFile})
    ENDIF()
  ENDFOREACH (_currentLibFile )
  SET( wxWidgets_LIBRARIES ${REVISED_wxWidgets_LIBRARIES})

  MESSAGE (STATUS " Revised wxWidgets Libraries: ${wxWidgets_LIBRARIES}")
ENDIF(NOT QT_ANDROID)


IF(OCPN_USE_CRASHREPORT)
  MESSAGE (STATUS "Crash reporting enabled")
  IF(MSVC)
    MESSAGE (STATUS "Using Windows CrashRpt")
    target_include_directories(${PACKAGE_NAME} PRIVATE
        ${CMAKE_SOURCE_DIR}/buildwin/crashrpt)
    target_link_libraries(${PACKAGE_NAME} PRIVATE
       ${CMAKE_SOURCE_DIR}/buildwin/crashrpt/CrashRpt1403.lib)
    SET(CMAKE_EXE_LINKER_FLAGS ${CMAKE_EXE_LINKER_FLAGS} /DEBUG)
  ELSE(MSVC)
    IF(NOT APPLE AND NOT QT_ANDROID AND NOT MINGW)
      IF(CMAKE_BUILD_TYPE MATCHES "Rel*")
        MESSAGE (STATUS "Using Linux crash reporting")
      ENDIF()
    ENDIF()
  ENDIF(MSVC)
ENDIF(OCPN_USE_CRASHREPORT)

add_subdirectory("libs/sqlite")
add_subdirectory("libs/SQLiteCpp")
target_link_libraries(${PACKAGE_NAME} PRIVATE ocpn::sqlite)
target_link_libraries(${PACKAGE_NAME} PRIVATE ocpn::sqlite_cpp)

IF(UNIX)
  IF(NOT QT_ANDROID) # this should be detected by the grib plugin
    FIND_PACKAGE(BZip2 REQUIRED)
    target_include_directories(${PACKAGE_NAME} PRIVATE ${BZIP2_INCLUDE_DIR})
    target_link_libraries(${PACKAGE_NAME} PRIVATE ${BZIP2_LIBRARIES})
    FIND_PACKAGE(ZLIB REQUIRED)
    target_include_directories(${PACKAGE_NAME} PRIVATE ${ZLIB_INCLUDE_DIR})
    target_link_libraries(${PACKAGE_NAME} PRIVATE ${ZLIB_LIBRARY})
    FIND_PACKAGE(X11)
    IF(X11_FOUND AND NOT APPLE)
      TARGET_LINK_LIBRARIES(${PACKAGE_NAME} PRIVATE ${X11_LIBRARIES} )
    ENDIF()
    IF(NOT APPLE)
      #TinyXML from Homebrew is not good enough for our GpxDocument needs
      #We should probably replace it with pugixml completely anyway...
      FIND_PACKAGE(TinyXML)
    ENDIF(NOT APPLE)
  ENDIF(NOT QT_ANDROID)
ENDIF(UNIX)


# check for lzma support for reading compressed charts
IF(CMAKE_HOST_WIN32)
  LIST(APPEND CMAKE_PREFIX_PATH "${CMAKE_CURRENT_SOURCE_DIR}/buildwin")
  LIST(APPEND CMAKE_PREFIX_PATH "${CMAKE_CURRENT_SOURCE_DIR}/buildwin/include")
ENDIF(CMAKE_HOST_WIN32)
IF(OCPN_USE_LZMA)
  FIND_PACKAGE(LibLZMA)
  IF(LIBLZMA_FOUND)
    message (STATUS "Building with lzma support")
    target_include_directories(${PACKAGE_NAME} PUBLIC ${LIBLZMA_INCLUDE_DIR})
  ELSE(LIBLZMA_FOUND)
    message (STATUS "lzma library not found")
  ENDIF(LIBLZMA_FOUND)
ENDIF(OCPN_USE_LZMA)

IF(OCPN_USE_CURL)
  IF(CMAKE_HOST_WIN32)
    INSTALL(FILES
        "${CMAKE_SOURCE_DIR}/buildwin/curl-ca-bundle.crt"
        "${CMAKE_SOURCE_DIR}/buildwin/libeay32.dll"
        "${CMAKE_SOURCE_DIR}/buildwin/ssleay32.dll"
        DESTINATION ".")
    IF(MSVC)
        SET(CURL_LIBRARIES "${CMAKE_SOURCE_DIR}/buildwin/libcurl.lib")
        INSTALL(FILES "buildwin/libcurl.dll" DESTINATION ".")
        INSTALL(FILES "buildwin/zlib1.dll" DESTINATION ".")
    ENDIF(MSVC)
  ELSE(CMAKE_HOST_WIN32)
    SET(OCPN_USE_EXTERN_CURL ON)
  ENDIF(CMAKE_HOST_WIN32)

  IF(NOT QT_ANDROID)
    IF(OCPN_USE_EXTERN_CURL)
      FIND_PACKAGE(CURL REQUIRED)
      target_include_directories(${PACKAGE_NAME} PRIVATE ${CURL_INCLUDE_DIRS})
    ELSE(OCPN_USE_EXTERN_CURL)
      INCLUDE("Curl")
      MESSAGE (STATUS "Using bundled curl library...")
    ENDIF(OCPN_USE_EXTERN_CURL)

    PKG_SEARCH_MODULE(SYS_WXCURL libwxcurl wxcurl)
    IF (SYS_WXCURL_FOUND AND USE_BUNDLED_LIBS MATCHES "OFF")
      message (STATUS "Building with system wxcurl")
      target_include_directories(${PACKAGE_NAME} PRIVATE
          ${CURL_INCLUDE_DIRS} ${SYS_WXCURL_INCLUDE_DIR})
      target_link_libraries(${PACKAGE_NAME} PRIVATE ${SYS_WXCURL_LIBRARIES})
    ELSE ()
      message (STATUS "Building with bundled wxcurl")
      include("Curl")
      add_subdirectory("libs/wxcurl")
      target_link_libraries(${PACKAGE_NAME} PRIVATE WXCURL)
    ENDIF ()

    target_link_libraries(${PACKAGE_NAME} PRIVATE ${CURL_LIBRARIES})

    IF(WIN32 AND OCPN_USE_EXTERN_CURL)
      FIND_LIBRARY(CURL_LIBRARY_DLL NAMES
          curl
          curllib
          libcurl_imp
          curllib_static
          libcurl
          PATH_SUFFIXES dll
      )
      INSTALL(FILES ${CURL_LIBRARY_DLL} DESTINATION ".")
    ENDIF(WIN32 AND OCPN_USE_EXTERN_CURL)
  ENDIF(NOT QT_ANDROID)
ENDIF(OCPN_USE_CURL)

IF(OCPN_USE_SVG)
  MESSAGE(STATUS "SVG support enabled...")
  IF(NOT QT_ANDROID)
    include(OcpnFindCairo)
    include(OcpnFindExpat)
    IF (CMAKE_HOST_WIN32)
      #On Windows, we have our own expat and cairo
      FILE(GLOB gtkdll_files "${CMAKE_CURRENT_SOURCE_DIR}/buildwin/gtk/*.dll")
      INSTALL(FILES ${gtkdll_files} DESTINATION ".")
      FILE(GLOB expatdll_files
           "${CMAKE_CURRENT_SOURCE_DIR}/buildwin/expat-2.2.5/*.dll")
      INSTALL(FILES ${expatdll_files} DESTINATION ".")
      MESSAGE(STATUS "GTK and Expat DLLs bundled into the installer package...")
    ELSE (CMAKE_HOST_WIN32)
      # Find FFMPEG - we have it disabled in our wxSVG
      #set(FFmpeg_FIND_COMPONENTS
      #        AVCODEC AVFORMAT AVUTIL SWSCALE SWRESAMPLE AVRESAMPLE)
      #unset(FFMPEG_LIBRARIES CACHE)
      #find_package(FFmpeg)
      #if (NOT AVCODEC_FOUND OR NOT AVFORMAT_FOUND
      #    OR NOT AVUTIL_FOUND OR NOT SWSCALE_FOUND
      #)
      #    message(FATAL_ERROR "FFmpeg component required, but not found!")
      #endif()
      #set(VIDEO_FFMPEG_LIBRARIES ${FFMPEG_LIBRARIES} ${SWSCALE_LIBRARIES})

      INCLUDE(FindEXIF)
      target_link_libraries(${PACKAGE_NAME} PRIVATE
          ${EXIF_LIBRARIES}
          ${GTK_LIBRARIES}
#         ${FFMPEG_LIBRARIES}
      )
      target_include_directories(${PACKAGE_NAME} PRIVATE
          ${EXIF_INCLUDE_DIRS}
#         ${FFMPEG_INCLUDE_DIRS}
      )
    ENDIF (CMAKE_HOST_WIN32)
    target_link_libraries(${PACKAGE_NAME} PRIVATE ocpn::cairo ocpn::expat)
 
    ADD_DEFINITIONS(-DocpnUSE_SVG)   # Goes into config.h, leave as global

    PKG_SEARCH_MODULE(WXSVG libwxsvg wxsvg)
    IF (WXSVG_FOUND AND USE_BUNDLED_LIBS MATCHES "OFF")
      message (STATUS "Building with system wxsvg includes")
      add_library(WXSVG INTERFACE)
      add_library(wxsvg::wxsvg ALIAS WXSVG)
      target_include_directories(WXSVG INTERFACE ${WXSVG_INCLUDE_DIR})
      target_link_libraries(WXSVG INTERFACE ${WXSVG_LIBRARIES})
    ELSE ()
      add_subdirectory("libs/wxsvg")
    ENDIF ()
    target_link_libraries(${PACKAGE_NAME} PRIVATE wxsvg::wxsvg)
  ENDIF(NOT QT_ANDROID)
ENDIF(OCPN_USE_SVG)

IF(OCPN_NEW_SERIAL)
  add_subdirectory("libs/serial")
  target_link_libraries(${PACKAGE_NAME} PRIVATE serial::serial)
  message(STATUS "Using new serial library...")
ENDIF(OCPN_NEW_SERIAL)


IF(OPENGL_FOUND)
    target_sources(${PACKAGE_NAME} PRIVATE
        include/glChartCanvas.h
        include/glTextureDescriptor.h
        include/glTexCache.h
        include/glTextureManager.h
        include/TexFont.h
        src/glTextureDescriptor.cpp
        src/glTexCache.cpp
        src/glChartCanvas.cpp
        src/glTextureManager.cpp
        src/TexFont.cpp
    )
ENDIF ()


#  Building for QT_ANDROID involves a cross-building environment,
#  So the include directories, flags, etc must be stated explicitly
#  without trying to locate them on the host build system.
IF(QT_ANDROID)
  MESSAGE (STATUS "Using GLESv1 for Android")
  ADD_DEFINITIONS(-DocpnUSE_GLES)
  ADD_DEFINITIONS(-DocpnUSE_GL)
  ADD_DEFINITIONS(-DARMHF)

  SET(OPENGLES_FOUND "YES")
  SET(OPENGL_FOUND "YES")

  # SET(wxWidgets_USE_LIBS ${wxWidgets_USE_LIBS} gl )
  add_subdirectory(libs/glshim)
ENDIF(QT_ANDROID)

MESSAGE (STATUS "    Adding local GLU" )
add_subdirectory(libs/glu)
SET( OPENGL_LIBRARIES "GLU_static" ${OPENGL_LIBRARIES})
MESSAGE (STATUS "    Revised GL Lib (with local): " ${OPENGL_LIBRARIES})

MESSAGE (STATUS "    Adding local LIBTESS2" )
add_subdirectory(libs/libtess2)
target_link_libraries(${PACKAGE_NAME} PRIVATE tess2::tess2)

Include("SoundConfig")
add_subdirectory(libs/sound)
target_link_libraries(${PACKAGE_NAME} PRIVATE sound::sound)

add_subdirectory(libs/wxJSON)
target_link_libraries(${PACKAGE_NAME} PRIVATE wxjson::wxjson)

IF(TINYXML_FOUND)
  message (STATUS "Building with system tinyxml")
  # FIXME: Add interface lib, use in garmin...
  add_library(TINYXML INTERFACE)
  target_include_directories(TINYXML INTERFACE ${TINYXML_INCLUDE_DIR})
  target_link_libraries(TINYXML INTERFACE ${TINYXML_LIBRARIES})
  add_library(tinyxml::tinyxml ALIAS TINYXML)
ELSE(TINYXML_FOUND)
  add_subdirectory(libs/tinyxml)
  target_link_libraries(${PACKAGE_NAME} PRIVATE tinyxml::tinyxml)
  message (STATUS "Building with bundled tinyxml")
ENDIF(TINYXML_FOUND)
set(TIXML_USE_STL 1)    # -> config.h

IF (USE_S57)
  message (STATUS "S57 ENC support: enabled")
  SET(SRC_S57ENC
        include/cm93.h
        include/mygeom.h
        include/ogr_s57.h
        include/Osenc.h
        include/s52plib.h
        include/s52utils.h
        include/s57chart.h
        include/s57RegistrarMgr.h
        include/SencManager.h
        include/TexFont.h
        src/cm93.cpp
        src/mygeom.cpp
        src/ogrs57datasource.cpp
        src/ogrs57layer.cpp
        src/Osenc.cpp
        src/s52cnsy.cpp
        src/s52plib.cpp
        src/s52utils.cpp
        src/s57chart.cpp
        src/s57classregistrar.cpp
        src/s57featuredefns.cpp
        src/s57obj.cpp
        src/s57reader.cpp
        src/s57RegistrarMgr.cpp
        src/SencManager.cpp
        src/TexFont.cpp
  )
  ADD_LIBRARY(S57ENC STATIC ${SRC_S57ENC})
  target_link_libraries(S57ENC PUBLIC
      nmea0183::nmea0183
      tess2::tess2
      wxsvg::wxsvg
      sound::sound
      wxjson::wxjson
      ssl::sha1
      tinyxml::tinyxml
      WXCURL
  )
  add_subdirectory(libs/iso8211)
  target_link_libraries(S57ENC PUBLIC iso8211::iso8211)

  add_subdirectory(libs/gdal)
  target_link_libraries(S57ENC PUBLIC gdal::gdal)

  target_include_directories(S57ENC PRIVATE ${CMAKE_SOURCE_DIR}/include)

  set_property(TARGET S57ENC PROPERTY COMPILE_FLAGS "${OBJ_VISIBILITY}")
  target_link_libraries(${PACKAGE_NAME} PRIVATE S57ENC)

ELSE (USE_S57)
  message (STATUS "S57 ENC support: disabled")
ENDIF (USE_S57)

IF (USE_GARMINHOST)
  add_subdirectory("libs/garmin")
  target_link_libraries(${PACKAGE_NAME} PUBLIC garminhost::garminhost)
  message(STATUS "Garmin Host Mode support: enabled")
ELSE (USE_GARMINHOST)
  message (STATUS "Garmin Host Mode support: disabled")
ENDIF (USE_GARMINHOST)

#Chart Symbols library
SET(SRC_SYMBOLS
    src/chartsymbols.cpp
    include/chartsymbols.h
)

ADD_LIBRARY(SYMBOLS STATIC ${SRC_SYMBOLS})
set_property(TARGET SYMBOLS PROPERTY COMPILE_FLAGS "${OBJ_VISIBILITY}")
target_link_libraries(SYMBOLS PRIVATE tinyxml::tinyxml)
target_include_directories(SYMBOLS PRIVATE ${CMAKE_SOURCE_DIR}/include)
target_link_libraries(${PACKAGE_NAME} PRIVATE SYMBOLS)

# Compile texcompression support library
IF (OPENGL_FOUND)
  add_subdirectory("libs/texcmp")
  target_link_libraries(${PACKAGE_NAME} PRIVATE texcmp::texcmp)

  add_subdirectory("libs/mipmap")
  target_link_libraries(${PACKAGE_NAME} PRIVATE mipmap::mipmap)
  pkg_search_module(LZ4 liblz4 lz4)
  if (LZ4_FOUND AND USE_BUNDLED_LIBS MATCHES "OFF")
      message(STATUS "Building with system lz4")
      target_include_directories(${PACKAGE_NAME} PUBLIC ${LZ4_INCLUDE_DIR})
      target_link_libraries(${PACKAGE_NAME} PUBLIC ${LZ4_LIBRARIES})
  else ()
      message(STATUS "Building with bundled lz4")
      add_subdirectory("libs/lz4")
      target_link_libraries(${PACKAGE_NAME} PUBLIC lz4::lz4)
  endif ()
ENDIF (OPENGL_FOUND)

if (MINGW)
    target_link_libraries(${PACKAGE_NAME} PRIVATE psapi winmm setupapi)
endif ()

#TODO
#dnl
#dnl Use OpenGL tesselator or Internal tesselator
#dnl
#tess_internal=false
#tess_glu=true
#AC_ARG_WITH(tess_internal,
#          [[  --with-tess-internal    use Internal Polygon Tesselator]],
#          [tess_internal=true]
#           )
#
#if [[ "$tess_internal" = "false" ]] ; then
#        dnl  Look for and qualify an external GLU library
#        echo "checking for useable OpenGLU Library"
#        AC_CHECK_LIB(GLU, gluNewTess, true, dnl here true is just a nop
#           AC_MSG_ERROR([*** libGLU not found.  Run configure using --with-tess-internal.]))
#
#        GL_LIBS="-lGL -lGLU"
#        AC_SUBST(GL_LIBS)
#
#        GL_CFLAGS="-DUSE_GLU_TESS"
#        AC_SUBST(GL_CFLAGS)
#fi

CONFIGURE_FILE(config.h.in ${CMAKE_BINARY_DIR}/include/config.h)
INCLUDE_DIRECTORIES(BEFORE "${CMAKE_BINARY_DIR}/include")

# If we build for windows systems, we also include the resource file
# containing the manifest, icon and other resources
IF(MSVC)
    target_sources(${PACKAGE_NAME} PRIVATE src/opencpn.rc)
ENDIF()

SET(gshhs_MIN
        data/gshhs/poly-c-1.dat
)

SET(gshhs_CRUDE ${gshhs_MIN}
        data/gshhs/wdb_borders_c.b
        data/gshhs/wdb_rivers_c.b
)

SET(gshhs_LOW ${gshhs_CRUDE}
        data/gshhs/poly-l-1.dat
        data/gshhs/wdb_borders_l.b
        data/gshhs/wdb_rivers_l.b
)

SET(gshhs_INTERMEDIATE ${gshhs_LOW}
        data/gshhs/poly-i-1.dat
        data/gshhs/wdb_borders_i.b
        data/gshhs/wdb_rivers_i.b
)

SET(gshhs_HIGH ${gshhs_INTERMEDIATE}
        data/gshhs/poly-h-1.dat
        data/gshhs/wdb_borders_h.b
        data/gshhs/wdb_rivers_h.b
)

SET(gshhs_FULL ${gshhs_HIGH}
        data/gshhs/poly-f-1.dat
        data/gshhs/wdb_borders_f.b
        data/gshhs/wdb_rivers_f.b
)

# Various data files
IF(BUNDLE_GSHHS MATCHES "MIN")
  SET(gshhs ${gshhs_MIN})
ENDIF(BUNDLE_GSHHS MATCHES "MIN")

IF(BUNDLE_GSHHS MATCHES "CRUDE")
  SET(gshhs ${gshhs_CRUDE})
ENDIF(BUNDLE_GSHHS MATCHES "CRUDE")

IF(BUNDLE_GSHHS MATCHES "LOW")
  SET(gshhs ${gshhs_LOW})
ENDIF(BUNDLE_GSHHS MATCHES "LOW")

IF(BUNDLE_GSHHS MATCHES "INTERMEDIATE")
  SET(gshhs ${gshhs_INTERMEDIATE})
ENDIF(BUNDLE_GSHHS MATCHES "INTERMEDIATE")

IF(BUNDLE_GSHHS MATCHES "HIGH")
  SET(gshhs ${gshhs_HIGH})
ENDIF(BUNDLE_GSHHS MATCHES "HIGH")

IF(BUNDLE_GSHHS MATCHES "FULL")
  SET(gshhs ${gshhs_FULL})
ENDIF(BUNDLE_GSHHS MATCHES "FULL")

SET(uiData
        src/bitmaps/styles.xml
        src/bitmaps/toolicons_journeyman.png
        src/bitmaps/toolicons_journeyman_flat.png
        src/bitmaps/toolicons_traditional.png
        src/bitmaps/iconRMinus.png
        src/bitmaps/iconRPlus.png
        src/bitmaps/iconMinimum.png
        src/bitmaps/iconStandard.png
        src/bitmaps/iconUserStd.png
        src/bitmaps/iconAll.png
        src/bitmaps/DragHandle.svg
        src/bitmaps/eye.svg
        src/bitmaps/eyex.svg
)

IF (USE_S57)
  SET(s57data
        data/s57data/attdecode.csv
        data/s57data/S52RAZDS.RLE
        data/s57data/s57attributes.csv
        data/s57data/s57expectedinput.csv
        data/s57data/s57objectclasses.csv
        data/s57data/rastersymbols-dark.png
        data/s57data/rastersymbols-day.png
        data/s57data/rastersymbols-dusk.png
        data/s57data/chartsymbols.xml
  )
ENDIF (USE_S57)

IF(BUNDLE_TCDATA MATCHES "ON")
  SET(tcData
    data/tcdata/HARMONIC
    data/tcdata/HARMONIC.IDX
    data/tcdata/README.harmonics
  )
ENDIF(BUNDLE_TCDATA MATCHES "ON")


IF(APPLE)
  target_sources(${PACKAGE_NAME} PRIVATE include/macutils.h src/macutils.c)
ENDIF ()

#  Mac has trouble finding libgobject-2.0.dylib
#  We look for it explicitely
IF(xAPPLE)
  # Assume pkg-config is available.
  PKG_SEARCH_MODULE( GOBJECT REQUIRED gobject-2.0 )
  # MESSAGE (STATUS "    GOBJECT: " ${GOBJECT_LDFLAGS})

  target_compile_definitions(${PACKAGE_NAME} PRIVATE ${GOBJECT_LDFLAGS})
  target_link_libraries(${PACKAGE_NAME} PUBLIC "iconv")
ENDIF(xAPPLE)

IF(WIN32)
  target_sources(${PACKAGE_NAME} PRIVATE app.manifest)
ENDIF(WIN32)

add_subdirectory("libs/nmea0183")
target_link_libraries(${PACKAGE_NAME} PUBLIC nmea0183::nmea0183)

IF(APPLE)
  IF(NOT BUNDLE_GSHHS MATCHES "NONE")
    SET_SOURCE_FILES_PROPERTIES(
             ${gshhs} PROPERTIES MACOSX_PACKAGE_LOCATION SharedSupport/gshhs  )
  ENDIF()

  SET_SOURCE_FILES_PROPERTIES(
             ${uiData} PROPERTIES MACOSX_PACKAGE_LOCATION SharedSupport/uidata  )
  SET_SOURCE_FILES_PROPERTIES(
             ${s57data} PROPERTIES MACOSX_PACKAGE_LOCATION SharedSupport/s57data  )
  IF(BUNDLE_TCDATA MATCHES "ON")
    SET_SOURCE_FILES_PROPERTIES(
             ${tcData} PROPERTIES MACOSX_PACKAGE_LOCATION SharedSupport/tcdata  )
  ENDIF(BUNDLE_TCDATA MATCHES "ON")

  SET_SOURCE_FILES_PROPERTIES(
             data/license.html PROPERTIES MACOSX_PACKAGE_LOCATION SharedSupport )

  SET_SOURCE_FILES_PROPERTIES(
             data/authors.html PROPERTIES MACOSX_PACKAGE_LOCATION SharedSupport )

  SET_SOURCE_FILES_PROPERTIES(
             data/opencpn.png PROPERTIES MACOSX_PACKAGE_LOCATION SharedSupport )

  SET_SOURCE_FILES_PROPERTIES(
             data/CoC-909_2013-InlandECDIS_20170308.pdf PROPERTIES MACOSX_PACKAGE_LOCATION SharedSupport )

  FILE(GLOB SOUND_FILES ${CMAKE_SOURCE_DIR}/data/sounds/*)
  FOREACH (_currentSoundFile ${SOUND_FILES})
    SET_SOURCE_FILES_PROPERTIES(
             ${_currentSoundFile} PROPERTIES MACOSX_PACKAGE_LOCATION SharedSupport/sounds )
  ENDFOREACH (_currentSoundFile )

  FILE(GLOB CONFIG_FILES ${CMAKE_SOURCE_DIR}/data/configs/*)
  FOREACH (_currentConfigFile ${CONFIG_FILES})
    SET_SOURCE_FILES_PROPERTIES(
           ${_currentConfigFile} PROPERTIES MACOSX_PACKAGE_LOCATION SharedSupport/configs )
  ENDFOREACH (_currentConfigFile )

  IF(OCPN_USE_SVG)
    #Traditional theme SVG icons
    FILE(GLOB TRADITIONAL_SVG_FILES "${CMAKE_SOURCE_DIR}/data/svg/traditional/*")
    FOREACH (_currentSvgFile ${TRADITIONAL_SVG_FILES})
      SET_SOURCE_FILES_PROPERTIES(
           ${_currentSvgFile} PROPERTIES MACOSX_PACKAGE_LOCATION SharedSupport/uidata/traditional )
    ENDFOREACH ()

    #Journeyman theme SVG icons
    FILE(GLOB JOURNEYMAN_SVG_FILES "${CMAKE_SOURCE_DIR}/data/svg/journeyman/*")
    FOREACH (_currentSvgFile ${JOURNEYMAN_SVG_FILES})
      SET_SOURCE_FILES_PROPERTIES(
              ${_currentSvgFile} PROPERTIES MACOSX_PACKAGE_LOCATION SharedSupport/uidata/journeyman )
    ENDFOREACH ()

    #Journeyman_flat theme SVG icons
    FILE(GLOB JOURNEYMAN_FLAT_SVG_FILES "${CMAKE_SOURCE_DIR}/data/svg/journeyman_flat/*")
    FOREACH (_currentSvgFile ${JOURNEYMAN_FLAT_SVG_FILES})
      SET_SOURCE_FILES_PROPERTIES(
              ${_currentSvgFile} PROPERTIES MACOSX_PACKAGE_LOCATION SharedSupport/uidata/journeyman_flat )
    ENDFOREACH ()

    #MUI_flat theme SVG icons
    FILE(GLOB MUI_FLAT_SVG_FILES "${CMAKE_SOURCE_DIR}/data/svg/MUI_flat/*")
    FOREACH (_currentSvgFile ${MUI_FLAT_SVG_FILES})
      SET_SOURCE_FILES_PROPERTIES(
            ${_currentSvgFile} PROPERTIES MACOSX_PACKAGE_LOCATION SharedSupport/uidata/MUI_flat )
    ENDFOREACH (_currentSvgFile )

    #Default SVG Icons
    FILE(GLOB DEFAULT_ICON_SVG_FILES "${CMAKE_SOURCE_DIR}/data/svg/markicons/*")
    FOREACH (_currentSvgFile ${DEFAULT_ICON_SVG_FILES})
      SET_SOURCE_FILES_PROPERTIES( ${_currentSvgFile} PROPERTIES MACOSX_PACKAGE_LOCATION SharedSupport/uidata/markicons )
    ENDFOREACH ()

  ENDIF(OCPN_USE_SVG)

  INSTALL(DIRECTORY data/svg/markicons/ DESTINATION ${PREFIX_PKGDATA}/uidata/markicons)

  INSTALL(DIRECTORY data/svg/MUI_flat/ DESTINATION ${PREFIX_PKGDATA}/uidata/MUI_flat)

  SET_SOURCE_FILES_PROPERTIES( data/doc/help_web.html PROPERTIES MACOSX_PACKAGE_LOCATION SharedSupport/doc )

  IF(BUNDLE_DOCS MATCHES "ON")
    SET_SOURCE_FILES_PROPERTIES( data/doc/help_en_US.html PROPERTIES MACOSX_PACKAGE_LOCATION SharedSupport/doc )

    FILE(GLOB_RECURSE WIKI_DOC_FILES "${CMAKE_SOURCE_DIR}/data/doc/*")
    FOREACH (_currentWikiDocFile ${WIKI_DOC_FILES})

      file(RELATIVE_PATH
      _docfileRelPath # Output variable
      "${CMAKE_SOURCE_DIR}/data/doc/" # Base directory
      ${_currentWikiDocFile} # Absolute path to the file
      )

      GET_FILENAME_COMPONENT(_docfileLocation ${_docfileRelPath} DIRECTORY)
      SET(_location "SharedSupport/doc/${_docfileLocation}")
  #    MESSAGE (STATUS "    Adding Wiki Doc File : " ${_currentWikiDocFile}  "  Destination: "  ${_location})
      SET_SOURCE_FILES_PROPERTIES( ${_currentWikiDocFile}  PROPERTIES MACOSX_PACKAGE_LOCATION ${_location} )
    ENDFOREACH (_currentWikiDocFile )


  ENDIF(BUNDLE_DOCS MATCHES "ON")

  SET_SOURCE_FILES_PROPERTIES( buildosx/opencpn.icns PROPERTIES MACOSX_PACKAGE_LOCATION Resources )

  SET_SOURCE_FILES_PROPERTIES( buildosx/MainMenu.xib PROPERTIES MACOSX_PACKAGE_LOCATION Resources/English.lproj )

  #  These variables get substituted into the Info.plist template file at build time
  SET(MACOSX_BUNDLE_ICON_FILE opencpn.icns)
  SET(MACOSX_BUNDLE_SHORT_VERSION_STRING "${PACKAGE_VERSION}")
  SET(MACOSX_BUNDLE_BUNDLE_NAME "OpenCPN")
  SET(MACOSX_BUNDLE_GUI_IDENTIFIER "org.opencpn")

  IF(BUNDLE_DOCS MATCHES "ON")
    SET(DOC_FILES
             data/doc/help_web.html
             data/doc/help_en_US.html
             ${WIKI_DOC_FILES}
    )
  ELSE(BUNDLE_DOCS MATCHES "ON")
    SET(DOC_FILES data/doc/help_web.html)
  ENDIF(BUNDLE_DOCS MATCHES "ON")

  target_sources(${PACKAGE_NAME} PRIVATE
        ${gshhs}
        ${uiData}
        ${s57data}
        ${tcData}
        data/license.html
        data/authors.html
        data/opencpn.png
        data/CoC-909_2013-InlandECDIS_20170308s.pdf
        ${SOUND_FILES}
        buildosx/opencpn.icns
        buildosx/MainMenu.xib
        ${TRADITIONAL_SVG_FILES}
        ${JOURNEYMAN_SVG_FILES}
        ${JOURNEYMAN_FLAT_SVG_FILES}
        ${DEFAULT_ICON_SVG_FILES}
        ${MUI_FLAT_SVG_FILES}
        ${DOC_FILES}
        ${CONFIG_FILES}
  )
  INSTALL(TARGETS ${PACKAGE_NAME} BUNDLE DESTINATION ${PREFIX_BIN} )

  TARGET_LINK_LIBRARIES(${PACKAGE_NAME} PRIVATE ${wxWidgets_LIBRARIES})
  set_target_properties(${PACKAGE_NAME} PROPERTIES MACOSX_BUNDLE_INFO_PLIST
                        "${CMAKE_SOURCE_DIR}/buildosx/Info.plist.in")
ENDIF(APPLE)

IF(QT_ANDROID)
  ADD_LIBRARY(gorp ${HDRS} ${SRCS}
          ${SRC_S57ENC}
          ${SRC_TEXCMP}
          ${SRC_MIPMAP}
          ${SRC_SYMBOLS}
  )
  TARGET_LINK_LIBRARIES( gorp PRIVATE NMEA0183 ${wxWidgets_LIBRARIES} )
ENDIF()

# Sanitizers, part 2/2
if ( CMAKE_VERSION VERSION_GREATER 3.4 )
  if (NOT "${ENABLE_SANITIZER}" MATCHES "none")
    target_link_libraries(${PACKAGE_NAME} -fsanitize=${ENABLE_SANITIZER})
  endif()
endif()

If(NOT APPLE AND NOT QT_ANDROID)
    TARGET_LINK_LIBRARIES(${PACKAGE_NAME} PRIVATE ${wxWidgets_LIBRARIES})
ENDIF()

IF(CMAKE_HOST_WIN32)
    TARGET_LINK_LIBRARIES(${PACKAGE_NAME} PRIVATE
          setupapi.lib
  #       glu32.lib
          "GLU_static"
          psapi.lib
          )

  # use gdi plus only on MSVC, it is not a free library
  IF(MSVC)
      TARGET_LINK_LIBRARIES(${PACKAGE_NAME} PRIVATE gdiplus.lib)
  ENDIF()

ENDIF(CMAKE_HOST_WIN32)

IF(MINGW)
  # assuming wxwidgets is compiled with unicode, this is needed for mingw headers
  TARGET_LINK_LIBRARIES(${PACKAGE_NAME} PRIVATE
    ${OPENGL_LIBRARIES}
    wsock32.lib
  )
ENDIF(MINGW)


IF(UNIX)
    TARGET_LINK_LIBRARIES(${PACKAGE_NAME} PRIVATE
        ${OPENGL_LIBRARIES}
        ${GETTEXT_LIBRARY}
        SQLITE_CPP
  )

  IF(NOT APPLE AND NOT QT_ANDROID AND NOT WIN32 AND NOT X11_FOUND)
    MESSAGE(STATUS "Did not find x11 libraries, support for transparent toolbar in opengl mode cannot be detected")
  ENDIF()

ENDIF(UNIX)

IF(LIBLZMA_FOUND)
    TARGET_LINK_LIBRARIES(${PACKAGE_NAME} PRIVATE ${LIBLZMA_LIBRARY} )
ENDIF(LIBLZMA_FOUND)

#   Certain older Cmake FindGTK2 modules ( e.g. cmake-2.8.0-2) do not yield all of the required link libraries
#   So, add them manually.  These declarations may be redundant in some architectures, but do no harm.
IF(UNIX)
    TARGET_LINK_LIBRARIES( ${PACKAGE_NAME} PRIVATE dl )
ENDIF ()


IF( UNIX AND NOT APPLE )
  FIND_PATH( LIBELF_INCLUDE_DIR NAMES libelf.h gelf.h PATH_SUFFIXES libelf )
  FIND_LIBRARY( LIBELF_LIBRARY NAMES elf )
  IF( LIBELF_INCLUDE_DIR AND LIBELF_LIBRARY )
    MESSAGE( STATUS "Found LibELF..." )
    MESSAGE( STATUS "    ELF Lib: ${LIBELF_INCLUDE_DIR}" )
    MESSAGE( STATUS "    ELF Include: ${LIBELF_LIBRARY}" )
    TARGET_INCLUDE_DIRECTORIES( ${PACKAGE_NAME} PUBLIC "${LIBELF_INCLUDE_DIR}" )
    TARGET_LINK_LIBRARIES( ${PACKAGE_NAME} PRIVATE "${LIBELF_LIBRARY}" )
  ELSE()
    MESSAGE( WARNING "Did not found LibELF, plugin compatibility check will be simplified." )
  ENDIF ()
ENDIF( UNIX AND NOT APPLE )

#
#  Install
#
INSTALL(FILES data/license.txt DESTINATION ${PREFIX_DATA}/${PACKAGE_NAME})

IF(NOT APPLE)

  IF(WIN32)
    INSTALL(TARGETS ${PACKAGE_NAME} RUNTIME DESTINATION ".")
  ENDIF()

  IF(UNIX AND NOT APPLE AND NOT QT_ANDROID)
    INSTALL(TARGETS ${PACKAGE_NAME} RUNTIME DESTINATION ${PREFIX_BIN})
    INSTALL(FILES LINUX_DEVICES.md DESTINATION ${PREFIX_DATA}/${PACKAGE_NAME})
  ENDIF()

  IF(WIN32)
    SET(PREFIX_PKGDATA ".")
  ENDIF()

  IF(NOT BUNDLE_GSHHS MATCHES "NONE")
    INSTALL(FILES ${gshhs} DESTINATION ${PREFIX_PKGDATA}/gshhs)
  ENDIF(NOT BUNDLE_GSHHS MATCHES "NONE")

  INSTALL(FILES ${uiData} DESTINATION ${PREFIX_PKGDATA}/uidata )
  INSTALL(DIRECTORY data/svg/markicons/ DESTINATION ${PREFIX_PKGDATA}/uidata/markicons)

  IF(OCPN_USE_SVG)
    #Traditional theme SVG icons
    INSTALL(DIRECTORY data/svg/traditional/ DESTINATION ${PREFIX_PKGDATA}/uidata/traditional)
    INSTALL(DIRECTORY data/svg/journeyman/ DESTINATION ${PREFIX_PKGDATA}/uidata/journeyman)
    INSTALL(DIRECTORY data/svg/journeyman_flat/ DESTINATION ${PREFIX_PKGDATA}/uidata/journeyman_flat)
    INSTALL(DIRECTORY data/svg/MUI_flat/ DESTINATION ${PREFIX_PKGDATA}/uidata/MUI_flat)
  ENDIF(OCPN_USE_SVG)

  IF (USE_S57)
      INSTALL(FILES ${s57data} DESTINATION ${PREFIX_PKGDATA}/s57data)
  ENDIF ()

  IF(BUNDLE_TCDATA MATCHES "ON")
    INSTALL(FILES ${tcData} DESTINATION ${PREFIX_PKGDATA}/tcdata)
  ENDIF()

  FILE(GLOB LICENSES "${CMAKE_SOURCE_DIR}/COPYING.*")
  INSTALL(FILES  ${LICENSES} LICENSING DESTINATION "${PREFIX_PKGDATA}")

  INSTALL(FILES data/CoC-909_2013-InlandECDIS_20170308s.pdf DESTINATION ${PREFIX_PKGDATA})
  INSTALL(FILES data/copyright DESTINATION ${PREFIX_DATA}/doc/${PACKAGE_NAME})

  INSTALL(FILES data/license.html DESTINATION ${PREFIX_PKGDATA})
  INSTALL(FILES data/authors.html DESTINATION ${PREFIX_PKGDATA})
  INSTALL(FILES data/opencpn.png DESTINATION ${PREFIX_PKGDATA})

  IF(PACKAGE_FORMAT EQUAL "DEB")
    INSTALL(FILES data/changelog.gz DESTINATION ${PREFIX_DATA}/doc/${PACKAGE_NAME})
  ELSE(PACKAGE_FORMAT EQUAL "DEB")
    INSTALL(FILES data/changelog DESTINATION ${PREFIX_DATA}/doc/${PACKAGE_NAME})
  ENDIF(PACKAGE_FORMAT EQUAL "DEB")

  INSTALL(DIRECTORY data/sounds/ DESTINATION ${PREFIX_PKGDATA}/sounds )

  IF(BUNDLE_DOCS MATCHES "ON")
    INSTALL(DIRECTORY data/doc/ DESTINATION ${PREFIX_PKGDATA}/doc )
  ELSE(BUNDLE_DOCS MATCHES "ON")
    INSTALL(FILES data/doc/help_web.html DESTINATION ${PREFIX_PKGDATA}/doc )
  ENDIF(BUNDLE_DOCS MATCHES "ON")

  IF(UNIX)
    INSTALL(FILES data/opencpn.png DESTINATION ${PREFIX_DATA}/icons/hicolor/48x48/apps)
    INSTALL(FILES src/bitmaps/opencpn-64.png DESTINATION ${PREFIX_DATA}/icons/hicolor/64x64/apps RENAME opencpn.png)
    INSTALL(FILES src/bitmaps/other_svg_src/opencpn.svg DESTINATION ${PREFIX_DATA}/icons/hicolor/scalable/apps)
    INSTALL(FILES data/opencpn.desktop DESTINATION ${PREFIX_DATA}/applications )
    INSTALL(FILES data/opencpn.appdata.xml DESTINATION ${PREFIX_DATA}/appdata )
    INSTALL(FILES opencpn.1 DESTINATION ${PREFIX_DATA}/man/man1)
  ENDIF(UNIX)

ENDIF(NOT APPLE)
#  Fix up time_t definition
# Checks for 32-bit version. And always use 32-bit time_t for compatibility


#uninstall-local:
#        rm -fr $(DESTDIR)$(pkgdatadir)/wvsdata/*
#        rm -fr $(DESTDIR)$(pkgdatadir)/gshhs/*
#        rm -fr $(DESTDIR) $(pkgdatadir)/s57data/*
#        rm -fr $(DESTDIR)$(pkgdatadir)/tcdata/*
#        rmdir $(DESTDIR)$(pkgdatadir)/wvsdata
#        rmdir $(DESTDIR)$(pkgdatadir)/gshhs
#        rmdir $(DESTDIR)$(pkgdatadir)/s57data
#        rmdir $(DESTDIR)$(pkgdatadir)/tcdata
#        rmdir $(DESTDIR)$(pkgdatadir)
#

#
# Gettext languages setup.
#
FIND_PROGRAM(GETTEXT_XGETTEXT_EXECUTABLE xgettext)
IF (GETTEXT_XGETTEXT_EXECUTABLE)

ADD_CUSTOM_COMMAND(
   OUTPUT ${CMAKE_SOURCE_DIR}/po/${PACKAGE_NAME}.pot.dummy
   COMMAND ${GETTEXT_XGETTEXT_EXECUTABLE} --from-code=iso-8859-1 --force-po --package-name=${PACKAGE_NAME} --package-version="${PACKAGE_VERSION}" --output=${CMAKE_SOURCE_DIR}/po/${PACKAGE_NAME}.pot  --keyword=_ --width=80 --files-from=${CMAKE_SOURCE_DIR}/po/POTFILES.in
   DEPENDS ${CMAKE_SOURCE_DIR}/po/POTFILES.in ${CMAKE_SOURCE_DIR}/po/${PACKAGE_NAME}.pot
   WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
   COMMENT "pot-update [${PACKAGE_NAME}]: Generated pot file."
)
ADD_CUSTOM_TARGET(pot-update COMMENT "pot-update: Done." DEPENDS ${CMAKE_SOURCE_DIR}/po/${PACKAGE_NAME}.pot.dummy)

ENDIF(GETTEXT_XGETTEXT_EXECUTABLE )

MACRO(GETTEXT_UPDATE_PO _potFile)
  SET(_poFiles ${_potFile})
  GET_FILENAME_COMPONENT(_absPotFile ${_potFile} ABSOLUTE)

  FOREACH (_currentPoFile ${ARGN})
    GET_FILENAME_COMPONENT(_absFile ${_currentPoFile} ABSOLUTE)
    GET_FILENAME_COMPONENT(_poBasename ${_absFile} NAME_WE)

    ADD_CUSTOM_COMMAND(
       OUTPUT ${_absFile}.dummy
       COMMAND ${GETTEXT_MSGMERGE_EXECUTABLE} --width=80 --strict --quiet --update --backup=none --no-location -s ${_absFile} ${_absPotFile}
       DEPENDS ${_absPotFile} ${_absFile}
       COMMENT "po-update [${_poBasename}]: Updated po file."
    )
    SET(_poFiles ${_poFiles} ${_absFile}.dummy)
  ENDFOREACH (_currentPoFile )

  ADD_CUSTOM_TARGET(po-update COMMENT "po-update: Done." DEPENDS ${_poFiles})
ENDMACRO(GETTEXT_UPDATE_PO)

IF (GETTEXT_MSGMERGE_EXECUTABLE)
  FILE(GLOB PACKAGE_PO_FILES ${CMAKE_SOURCE_DIR}/po/*.po)
  GETTEXT_UPDATE_PO(${CMAKE_SOURCE_DIR}/po/${PACKAGE_NAME}.pot ${PACKAGE_PO_FILES})
ENDIF()

SET(_gmoFiles)
MACRO(GETTEXT_BUILD_MO _poFile _lang)

  GET_FILENAME_COMPONENT(_absFile ${_poFile} ABSOLUTE)
  GET_FILENAME_COMPONENT(_poBasename ${_absFile} NAME_WE)
  SET(_gmoFile ${CMAKE_CURRENT_BINARY_DIR}/${_poBasename}.mo)

  ADD_CUSTOM_COMMAND(
     OUTPUT ${_gmoFile}
     COMMAND ${GETTEXT_MSGFMT_EXECUTABLE} --check -o ${_gmoFile} ${_absFile}
     COMMAND ${CMAKE_COMMAND} -E copy  ${_gmoFile}
         ${CMAKE_CURRENT_BINARY_DIR}/Resources/${PACKAGE_NAME}_${_lang}.lproj/${PACKAGE_NAME}.mo
     DEPENDS ${_absFile}
     COMMENT "i18n [${_poBasename}]: Created mo file."
  )
  IF(APPLE)
    INSTALL(FILES ${_gmoFile} DESTINATION
      ${CMAKE_INSTALL_PREFIX}/bin/OpenCPN.app/Contents/Resources/${_lang}.lproj
      RENAME ${PACKAGE_NAME}.mo
    )
  ELSE()
    INSTALL(FILES ${_gmoFile} DESTINATION
      ${PREFIX_DATA}/locale/${_lang}/LC_MESSAGES RENAME ${PACKAGE_NAME}.mo
    )
  ENDIF()

  SET(_gmoFiles ${_gmoFiles} ${_gmoFile})
ENDMACRO(GETTEXT_BUILD_MO)

if(GETTEXT_MSGFMT_EXECUTABLE)
  GETTEXT_BUILD_MO(po/opencpn_cs_CZ.po cs)
  GETTEXT_BUILD_MO(po/opencpn_da_DK.po da)
  GETTEXT_BUILD_MO(po/opencpn_de_DE.po de)
  GETTEXT_BUILD_MO(po/opencpn_es_ES.po es)
  GETTEXT_BUILD_MO(po/opencpn_fr_FR.po fr)
  GETTEXT_BUILD_MO(po/opencpn_it_IT.po it)
  GETTEXT_BUILD_MO(po/opencpn_nl_NL.po nl)
  GETTEXT_BUILD_MO(po/opencpn_pl_PL.po pl)
  GETTEXT_BUILD_MO(po/opencpn_ru_RU.po ru)
  GETTEXT_BUILD_MO(po/opencpn_sv_SE.po sv)
  GETTEXT_BUILD_MO(po/opencpn_et_EE.po et)
  GETTEXT_BUILD_MO(po/opencpn_pt_PT.po pt_PT)
  GETTEXT_BUILD_MO(po/opencpn_pt_BR.po pt_BR)
  GETTEXT_BUILD_MO(po/opencpn_nb_NO.po nb_NO)
  GETTEXT_BUILD_MO(po/opencpn_tr_TR.po tr_TR)
  GETTEXT_BUILD_MO(po/opencpn_fi_FI.po fi_FI)
  GETTEXT_BUILD_MO(po/opencpn_el_GR.po el_GR)
  GETTEXT_BUILD_MO(po/opencpn_zh_TW.po zh_TW)
  GETTEXT_BUILD_MO(po/opencpn_zh_CN.po zh_TW)
  GETTEXT_BUILD_MO(po/opencpn_hu_HU.po hu_HU)
  GETTEXT_BUILD_MO(po/opencpn_gl_ES.po gl_ES)
  GETTEXT_BUILD_MO(po/opencpn_ca_ES.po ca_ES)
  GETTEXT_BUILD_MO(po/opencpn_ar_SA.po ar_SA)
  GETTEXT_BUILD_MO(po/opencpn_ja_JP.po ja_JP)
  GETTEXT_BUILD_MO(po/opencpn_vi_VN.po vi_VN)
  ADD_CUSTOM_TARGET(i18n COMMENT "i18n: Done." DEPENDS ${_gmoFiles})

  IF(NOT QT_ANDROID)
    ADD_DEPENDENCIES(${PACKAGE_NAME} i18n)
  ENDIF()

ENDIF(GETTEXT_MSGFMT_EXECUTABLE)

IF(WIN32 AND NOT UNIX)
  IF(NOT SKIP_VERSION_CONFIG)
    configure_file(${CMAKE_SOURCE_DIR}/src/opencpn.rc.in ${CMAKE_SOURCE_DIR}/src/opencpn.rc)
    configure_file("${CMAKE_SOURCE_DIR}/buildwin/NSIS_Unicode/Language files/Langstrings_GERMAN.nsh.in"
      "${CMAKE_SOURCE_DIR}//buildwin/NSIS_Unicode/Include/Langstrings_GERMAN.nsh" @ONLY)
    configure_file("${CMAKE_SOURCE_DIR}/buildwin/NSIS_Unicode/Language files/Langstrings_FRENCH.nsh.in"
      "${CMAKE_SOURCE_DIR}//buildwin/NSIS_Unicode/Include/Langstrings_FRENCH.nsh" @ONLY)
    configure_file("${CMAKE_SOURCE_DIR}/buildwin/NSIS_Unicode/Language files/Langstrings_CZECH.nsh.in"
      "${CMAKE_SOURCE_DIR}//buildwin/NSIS_Unicode/Include/Langstrings_CZECH.nsh" @ONLY)
    configure_file("${CMAKE_SOURCE_DIR}/buildwin/NSIS_Unicode/Language files/Langstrings_DANISH.nsh.in"
      "${CMAKE_SOURCE_DIR}//buildwin/NSIS_Unicode/Include/Langstrings_DANISH.nsh" @ONLY)
    configure_file("${CMAKE_SOURCE_DIR}/buildwin/NSIS_Unicode/Language files/Langstrings_SPANISH.nsh.in"
      "${CMAKE_SOURCE_DIR}//buildwin/NSIS_Unicode/Include/Langstrings_SPANISH.nsh" @ONLY)
    configure_file("${CMAKE_SOURCE_DIR}/buildwin/NSIS_Unicode/Language files/Langstrings_ITALIAN.nsh.in"
      "${CMAKE_SOURCE_DIR}//buildwin/NSIS_Unicode/Include/Langstrings_ITALIAN.nsh" @ONLY)
    configure_file("${CMAKE_SOURCE_DIR}/buildwin/NSIS_Unicode/Language files/Langstrings_DUTCH.nsh.in"
      "${CMAKE_SOURCE_DIR}//buildwin/NSIS_Unicode/Include/Langstrings_DUTCH.nsh" @ONLY)
    configure_file("${CMAKE_SOURCE_DIR}/buildwin/NSIS_Unicode/Language files/Langstrings_POLISH.nsh.in"
      "${CMAKE_SOURCE_DIR}//buildwin/NSIS_Unicode/Include/Langstrings_POLISH.nsh" @ONLY)
    configure_file("${CMAKE_SOURCE_DIR}/buildwin/NSIS_Unicode/Language files/Langstrings_PORTUGUESEBR.nsh.in"
      "${CMAKE_SOURCE_DIR}//buildwin/NSIS_Unicode/Include/Langstrings_PORTUGUESEBR.nsh" @ONLY)
    configure_file("${CMAKE_SOURCE_DIR}/buildwin/NSIS_Unicode/Language files/Langstrings_PORTUGUESE.nsh.in"
      "${CMAKE_SOURCE_DIR}//buildwin/NSIS_Unicode/Include/Langstrings_PORTUGUESE.nsh" @ONLY)
    configure_file("${CMAKE_SOURCE_DIR}/buildwin/NSIS_Unicode/Language files/Langstrings_RUSSIAN.nsh.in"
      "${CMAKE_SOURCE_DIR}//buildwin/NSIS_Unicode/Include/Langstrings_RUSSIAN.nsh" @ONLY)
    configure_file("${CMAKE_SOURCE_DIR}/buildwin/NSIS_Unicode/Language files/Langstrings_SWEDISH.nsh.in"
      "${CMAKE_SOURCE_DIR}//buildwin/NSIS_Unicode/Include/Langstrings_SWEDISH.nsh" @ONLY)
    configure_file("${CMAKE_SOURCE_DIR}/buildwin/NSIS_Unicode/Language files/Langstrings_FINNISH.nsh.in"
      "${CMAKE_SOURCE_DIR}//buildwin/NSIS_Unicode/Include/Langstrings_FINNISH.nsh" @ONLY)
    configure_file("${CMAKE_SOURCE_DIR}/buildwin/NSIS_Unicode/Language files/Langstrings_NORWEGIAN.nsh.in"
      "${CMAKE_SOURCE_DIR}//buildwin/NSIS_Unicode/Include/Langstrings_NORWEGIAN.nsh" @ONLY)
    configure_file("${CMAKE_SOURCE_DIR}/buildwin/NSIS_Unicode/Language files/Langstrings_CHINESETW.nsh.in"
      "${CMAKE_SOURCE_DIR}//buildwin/NSIS_Unicode/Include/Langstrings_CHINESETW.nsh" @ONLY)
    configure_file("${CMAKE_SOURCE_DIR}/buildwin/NSIS_Unicode/Language files/Langstrings_TURKISH.nsh.in"
      "${CMAKE_SOURCE_DIR}//buildwin/NSIS_Unicode/Include/Langstrings_TURKISH.nsh" @ONLY)
  ENDIF(NOT SKIP_VERSION_CONFIG)
ENDIF(WIN32 AND NOT UNIX)

#
#   Install wxstd.mo in Windows and Mac
#
IF(WIN32 OR APPLE)

  FILE(GLOB WXWIDGETS_MO_FILES ${CMAKE_SOURCE_DIR}/buildwin/wxWidgets/locale/*.mo)

  FOREACH ( _current_wx_mofile ${WXWIDGETS_MO_FILES})
    GET_FILENAME_COMPONENT(_absFile ${_current_wx_mofile} ABSOLUTE)
    GET_FILENAME_COMPONENT(_mlang ${_absFile} NAME_WE)

    IF(APPLE)
      INSTALL(FILES ${_current_wx_mofile} DESTINATION
	${CMAKE_INSTALL_PREFIX}/bin/OpenCPN.app/Contents/Resources/${_mlang}.lproj
	RENAME wxstd.mo
    )
    ELSE(APPLE)
      INSTALL(FILES ${_current_wx_mofile} DESTINATION
	${PREFIX_DATA}/locale/${_mlang}/LC_MESSAGES
       	RENAME wxstd.mo
       	PERMISSIONS OWNER_READ OWNER_WRITE GROUP_READ WORLD_READ
      )
    ENDIF(APPLE)

  ENDFOREACH (_current_wx_mofile )

ENDIF(WIN32 OR APPLE)


#
# On Windows builds, copy the wxWidgets dlls and helpers to the executable directory
#
IF(WIN32 AND NOT UNIX)
  IF(BUNDLE_WXDLLS)
    FILE(GLOB wxdll_files "${CMAKE_CURRENT_SOURCE_DIR}/buildwin/wxWidgets/*.dll")
    INSTALL(FILES ${wxdll_files} DESTINATION ".")
    MESSAGE(STATUS "wxWidgets DLLs bundled into the installer package...")
  ENDIF(BUNDLE_WXDLLS)
  IF(BUNDLE_VCDLLS)
    FILE(GLOB vcdll_files "${CMAKE_CURRENT_SOURCE_DIR}/buildwin/vc/*.dll")
    INSTALL(FILES ${vcdll_files} DESTINATION ".")
    MESSAGE(STATUS "MSVC redistributable DLLs bundled into the installer package...")
  ENDIF(BUNDLE_VCDLLS)
  OPTION (BUNDLE_LIBARCHIVEDLLS "Bundle the prebuilt LibArchive and LibLZMA DLLs" ON)
  IF(BUNDLE_LIBARCHIVEDLLS)
    FILE(GLOB libarchivedll_files
	 "${CMAKE_CURRENT_SOURCE_DIR}/buildwin/archive.dll"
	 "${CMAKE_CURRENT_SOURCE_DIR}/buildwin/liblzma.dll"
	 "${CMAKE_CURRENT_SOURCE_DIR}/buildwin/zlib1.dll"
    )
    INSTALL(FILES ${libarchivedll_files} DESTINATION ".")
    MESSAGE(STATUS "LibArchive DLLs bundled into the installer package...")
  ENDIF(BUNDLE_LIBARCHIVEDLLS)
ENDIF(WIN32 AND NOT UNIX)

IF(MSVC AND OCPN_USE_CRASHREPORT)
  INSTALL(FILES ${CMAKE_SOURCE_DIR}/buildwin/crashrpt/CrashRpt1403.dll
    DESTINATION ${PREFIX_PKGDATA}
  )
  INSTALL(FILES ${CMAKE_SOURCE_DIR}/buildwin/crashrpt/CrashSender1403.exe
    DESTINATION ${PREFIX_PKGDATA}
  )
  INSTALL(FILES ${CMAKE_SOURCE_DIR}/buildwin/crashrpt/crashrpt_lang.ini
    DESTINATION ${PREFIX_PKGDATA}
  )
  INSTALL(FILES ${CMAKE_SOURCE_DIR}/buildwin/crashrpt/dbghelp.dll
    DESTINATION ${PREFIX_PKGDATA}
  )
  INSTALL(FILES ${CMAKE_SOURCE_DIR}/buildwin/crashrpt/PrivacyPolicy.txt
    DESTINATION ${PREFIX_PKGDATA}
  )
  MESSAGE(STATUS "CrashRpt bundled into the installer package...")
ENDIF(MSVC AND OCPN_USE_CRASHREPORT)

MESSAGE (STATUS "")

IF(NOT SKIP_PLUGINS)
  add_subdirectory (plugins)
ENDIF ()

#
#  RPM package setup
#
IF(BUNDLE_GSHHS MATCHES "NONE|MIN|CRUDE|LOW|INTERMEDIATE|HIGH|FULL")
  # Possible values: NONE (default), MIN (=Only crude polygons, no borders/rivers),
  # CRUDE, LOW, INTERMEDIATE, HIGH, FULL
  MESSAGE (STATUS "*** Package will include GSHHS basechart level: ${BUNDLE_GSHHS} ***")
  IF(UNIX AND NOT APPLE)
    IF(BUNDLE_GSHHS MATCHES "NONE")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_min OFF")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_crude OFF")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_low OFF")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_intermediate OFF")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_high OFF")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_full OFF")
    ENDIF(BUNDLE_GSHHS MATCHES "NONE")
    IF(BUNDLE_GSHHS MATCHES "MIN")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_min ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_crude OFF")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_low OFF")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_intermediate OFF")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_high OFF")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_full OFF")
    ENDIF(BUNDLE_GSHHS MATCHES "MIN")
    IF(BUNDLE_GSHHS MATCHES "CRUDE")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_min ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_crude ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_low OFF")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_intermediate OFF")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_high OFF")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_full OFF")
    ENDIF(BUNDLE_GSHHS MATCHES "CRUDE")
    IF(BUNDLE_GSHHS MATCHES "LOW")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_min ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_crude ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_low ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_intermediate OFF")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_high OFF")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_full OFF")
    ENDIF(BUNDLE_GSHHS MATCHES "LOW")
    IF(BUNDLE_GSHHS MATCHES "INTERMEDIATE")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_min ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_crude ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_low ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_intermediate ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_high OFF")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_full OFF")
    ENDIF(BUNDLE_GSHHS MATCHES "INTERMEDIATE")
    IF(BUNDLE_GSHHS MATCHES "HIGH")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_min ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_crude ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_low ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_intermediate ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_high ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_full OFF")
    ENDIF(BUNDLE_GSHHS MATCHES "HIGH")
    IF(BUNDLE_GSHHS MATCHES "FULL")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_min ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_crude ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_low ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_intermediate ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_high ON")
      SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_full ON")
    ENDIF(BUNDLE_GSHHS MATCHES "FULL")
  ENDIF(UNIX AND NOT APPLE)
ELSE(BUNDLE_GSHHS MATCHES "NONE|MIN|CRUDE|LOW|INTERMEDIATE|HIGH|FULL")
  SET(BUNDLE_GSHHS "NONE")
  MESSAGE (STATUS "*** Package will NOT include GSHHS data ***")
  IF(UNIX AND NOT APPLE)
    SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_min OFF")
    SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_crude OFF")
    SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_low OFF")
    SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_intermediate OFF")
    SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_high OFF")
    SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_gshhs_full OFF")
    SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_tcdata OFF")
  ENDIF(UNIX AND NOT APPLE)
ENDIF(BUNDLE_GSHHS MATCHES "NONE|MIN|CRUDE|LOW|INTERMEDIATE|HIGH|FULL")

IF(BUNDLE_TCDATA MATCHES "ON")
  MESSAGE (STATUS "*** Package will include tide and current data ***")
  IF(UNIX AND NOT APPLE)
    SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_tcdata ON")
  ENDIF(UNIX AND NOT APPLE)
ELSE(BUNDLE_TCDATA MATCHES "ON")
  MESSAGE (STATUS "*** Package will NOT include tide and current data ***")
  IF(UNIX AND NOT APPLE)
    SET(CPACK_RPM_SPEC_MORE_DEFINE "${CPACK_RPM_SPEC_MORE_DEFINE}\n%define _include_tcdata OFF")
  ENDIF(UNIX AND NOT APPLE)
ENDIF(BUNDLE_TCDATA MATCHES "ON")


#
# build a CPack driven installer package
#

#include (InstallRequiredSystemLibraries)
SET(CPACK_PACKAGE_NAME "OpenCPN")
SET(CPACK_PACKAGE_VENDOR "opencpn.org")
SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "OpenCPN ${PACKAGE_VERSION}")
SET(CPACK_PACKAGE_VERSION ${PACKAGE_VERSION})
SET(CPACK_PACKAGE_VERSION_MAJOR ${VERSION_MAJOR})
SET(CPACK_PACKAGE_VERSION_MINOR ${VERSION_MINOR})
SET(CPACK_PACKAGE_VERSION_PATCH ${VERSION_PATCH}${VERSION_TAIL})
SET(CPACK_INSTALL_CMAKE_PROJECTS "${CMAKE_CURRENT_BINARY_DIR};${PACKAGE_NAME};ALL;/")
SET(CPACK_PACKAGE_EXECUTABLES ${PACKAGE_NAME} "OpenCPN")

#
#  Windows package setup.
#
IF(WIN32 AND NOT UNIX)
  # There is a bug in NSI that does not handle full unix paths properly. Make
  # sure there is at least one set of four (4) backlasshes.

  SET(CPACK_NSIS_INSTALLED_ICON_NAME "opencpn.exe")
  SET(CPACK_NSIS_PACKAGE_NAME_LC "opencpn")

#  These lines set the name of the Windows Start Menu shortcut and the icon that goes with it
  SET(CPACK_NSIS_DISPLAY_NAME "${CPACK_PACKAGE_NAME} ${PACKAGE_VERSION}")
  SET(CPACK_PACKAGE_ICON "${CMAKE_SOURCE_DIR}/src/bitmaps\\\\opencpn.ico")

SET(CPACK_PACKAGE_FILE_NAME "${PACKAGE_NAME}_${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}${VERSION_TAIL}_setup" )

  SET(CPACK_NSIS_DIR "${CMAKE_SOURCE_DIR}/buildwin/NSIS_Unicode")  #Gunther
  SET(CPACK_BUILDWIN_DIR "${CMAKE_SOURCE_DIR}/buildwin")  #Gunther

  SET(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/data/license.txt")

  IF(MINGW)
    SET(CPACK_STRIP_FILES "bin/opencpn")
  ENDIF(MINGW)

ELSE(WIN32 AND NOT UNIX)
  SET(CPACK_PACKAGE_INSTALL_DIRECTORY ${PACKAGE_NAME})
  SET(CPACK_STRIP_FILES "bin/opencpn")
  SET(CPACK_SOURCE_STRIP_FILES "")
  SET(CPACK_PACKAGE_FILE_NAME "${PACKAGE_NAME}_${PACKAGE_VERSION}-${PACKAGE_RELEASE}_${ARCH}" )
  SET(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/data/license.html")

ENDIF(WIN32 AND NOT UNIX)

IF (EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/README")
    SET(CPACK_PACKAGE_DESCRIPTION_FILE "${CMAKE_CURRENT_SOURCE_DIR}/README")
    SET(CPACK_RESOURCE_FILE_README "${CMAKE_CURRENT_SOURCE_DIR}/README")
ENDIF ()

SET(CPACK_SOURCE_GENERATOR "TGZ")

# The following components are regex's to match anywhere (unless anchored)
# in absolute path + filename to find files or directories to be excluded
# from source tarball.
set(CPACK_SOURCE_IGNORE_FILES
"\\\\.cvsignore$"
"^${CMAKE_CURRENT_SOURCE_DIR}.*/CVS/"
"^${CMAKE_CURRENT_SOURCE_DIR}/build*"
"^${CPACK_PACKAGE_INSTALL_DIRECTORY}/*"
)

#
#  Debian package setup
#
IF(UNIX AND NOT APPLE)
  #    INCLUDE(UseRPMTools)
  #    IF(RPMTools_FOUND)
  #        RPMTools_ADD_RPM_TARGETS(packagename ${CMAKE_SOURCE_DIR}/package.spec)
  #    ENDIF(RPMTools_FOUND)

  SET(CPACK_STRIP_FILES ON)
  SET(CPACK_GENERATOR ${PACKAGE_FORMAT})
  SET(CPACK_PACKAGE_CONTACT "David S. Register ")

  SET(CPACK_DEBIAN_PACKAGE_DEPENDS ${PACKAGE_DEPS})
  # autogenerate additional dependency information
  SET (CPACK_DEBIAN_PACKAGE_SHLIBDEPS ON)
  SET(CPACK_DEBIAN_PACKAGE_RECOMMENDS ${PACKAGE_RECS})
  SET(CPACK_DEBIAN_PACKAGE_ARCHITECTURE ${ARCH})
  SET(CPACK_DEBIAN_PACKAGE_VERSION "${CPACK_PACKAGE_VERSION}")
  SET(CPACK_DEBIAN_PACKAGE_SECTION "misc")
  SET(CPACK_DEBIAN_COMPRESSION_TYPE "xz")
  SET(CPACK_RPM_PACKAGE_VERSION "${CPACK_PACKAGE_VERSION}")
  SET(CPACK_RPM_PACKAGE_RELEASE "${PACKAGE_RELEASE}")
  SET(CPACK_RPM_PACKAGE_ARCHITECTURE  ${ARCH})
  SET(CPACK_RPM_PACKAGE_REQUIRES  ${PACKAGE_DEPS})
  SET(CPACK_RPM_PACKAGE_RELOCATABLE OFF)
  SET(CPACK_RPM_USER_BINARY_SPECFILE "${CMAKE_SOURCE_DIR}/opencpn.spec.in")
  SET(CPACK_OPENCPN_RPM_BINDIR "${CMAKE_INSTALL_PREFIX}/${PREFIX_BIN}")
  SET(CPACK_OPENCPN_RPM_LIBDIR "${PREFIX_LIB}")
  SET(CPACK_OPENCPN_RPM_DATADIR "${CMAKE_INSTALL_PREFIX}/${PREFIX_DATA}")
  SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "OpenSource Chart Plotter/Navigator")
  SET(CPACK_PACKAGE_DESCRIPTION "OpenCPN is a concise ChartPlotter/Navigator. The application supports: GPS/GPDS Position Input, BSB Raster Chart Display, S57 Vector ENChart Display, AIS Input Decoding, Waypoint Autopilot Navigation .")
  SET(CPACK_PACKAGE_RELOCATABLE OFF)
  SET(CPACK_SET_DESTDIR ON)
  SET(CPACK_INSTALL_PREFIX "${CMAKE_INSTALL_PREFIX}")
ENDIF(UNIX AND NOT APPLE)

# this dummy target is necessary to make sure the ADDITIONAL_MAKE_CLEAN_FILES directive is executed.
# apparently, the base CMakeLists.txt file must have "some" target to activate all the clean steps.
ADD_CUSTOM_TARGET(dummy COMMENT "dummy: Done." DEPENDS ${PACKAGE_NAME})

SET_DIRECTORY_PROPERTIES(dummy ADDITIONAL_MAKE_CLEAN_FILES ${CMAKE_SOURCE_DIR}/include/config.h)

INCLUDE(CPack)

#
# Apple application bundle
#
IF(APPLE)
# -- Run the BundleUtilities cmake code
  SET(CPACK_BUNDLE_PLIST "${CMAKE_SOURCE_DIR}/buildosx/Info.plist.in")

  SET(APPS "\${CMAKE_INSTALL_PREFIX}/bin/OpenCPN.app")
  SET(DIRS "")

#INSTALL(DIRECTORY DESTINATION "bin/OpenCPN.app/Contents/PlugIns")
  INSTALL(FILES ${CMAKE_CURRENT_BINARY_DIR}/plugins/dashboard_pi/libdashboard_pi.dylib DESTINATION "bin/OpenCPN.app/Contents/PlugIns")
  INSTALL(FILES ${CMAKE_CURRENT_BINARY_DIR}/plugins/grib_pi/libgrib_pi.dylib DESTINATION "bin/OpenCPN.app/Contents/PlugIns")
  INSTALL(FILES ${CMAKE_CURRENT_BINARY_DIR}/plugins/chartdldr_pi/libchartdldr_pi.dylib DESTINATION "bin/OpenCPN.app/Contents/PlugIns")
  INSTALL(FILES ${CMAKE_CURRENT_BINARY_DIR}/plugins/wmm_pi/libwmm_pi.dylib DESTINATION "bin/OpenCPN.app/Contents/PlugIns")

  SET(LIBS "\${CMAKE_INSTALL_PREFIX}/bin/OpenCPN.app/Contents/PlugIns/libdashboard_pi.dylib")
  SET(LIBS ${LIBS} "\${CMAKE_INSTALL_PREFIX}/bin/OpenCPN.app/Contents/PlugIns/libgrib_pi.dylib")
  SET(LIBS ${LIBS} "\${CMAKE_INSTALL_PREFIX}/bin/OpenCPN.app/Contents/PlugIns/libchartdldr_pi.dylib")
  SET(LIBS ${LIBS} "\${CMAKE_INSTALL_PREFIX}/bin/OpenCPN.app/Contents/PlugIns/libwmm_pi.dylib")

  INSTALL(CODE "
    include(BundleUtilities)
    fixup_bundle(\"${APPS}\"   \"${LIBS}\"   \"${DIRS}\")
    " COMPONENT Runtime
  )

  ADD_CUSTOM_COMMAND(
OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/${PACKAGE_NAME}_${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}${VERSION_TAIL}.dmg
COMMAND ${CMAKE_SOURCE_DIR}/buildosx/create-dmg --volname "OpenCPN Installer" --background ${CMAKE_SOURCE_DIR}/buildosx/background.png --window-pos 200 120 --window-size 500 300 --icon-size 80 --icon OpenCPN.app 120 150 --hide-extension OpenCPN.app --app-drop-link 390 145 ${CMAKE_CURRENT_BINARY_DIR}/${PACKAGE_NAME}_${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}${VERSION_TAIL}.dmg ${CMAKE_INSTALL_PREFIX}/bin/
    DEPENDS ${CMAKE_INSTALL_PREFIX}/bin/OpenCPN.app
    WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
    COMMENT "create-dmg [${PACKAGE_NAME}]: Generated dmg file."
  )

  ADD_CUSTOM_TARGET(create-dmg COMMENT "create-dmg: Done." DEPENDS
${CMAKE_CURRENT_BINARY_DIR}/${PACKAGE_NAME}_${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}${VERSION_TAIL}.dmg
  )
ENDIF(APPLE)


IF(MSVC)
  ADD_CUSTOM_COMMAND(TARGET ${PACKAGE_NAME}
    POST_BUILD
    COMMAND cmake -E copy "${CMAKE_BUILD_TYPE}/opencpn.pdb" "${CMAKE_BUILD_TYPE}/${PACKAGE_NAME}-${PACKAGE_VERSION}.pdb")
ENDIF(MSVC)

MESSAGE(STATUS "Writing spec file...")
